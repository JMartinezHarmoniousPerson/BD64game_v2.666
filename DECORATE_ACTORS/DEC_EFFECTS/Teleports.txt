ACTOR TeleportationFog : TeleportFog Replaces TeleportFog		//r30.10 revised to vanilla
{
Scale 1.25
Health 100
Radius 0
Height 0
Mass 999999
Renderstyle Add
DamageFactor "CancelTeleportFog",9999.0
+THRUACTORS
+SHOOTABLE
+NOBLOOD
+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay Radius_Quake(3,3,0,10,0)
		TNT1 A 0 A_PlaySound("DSTELEPT",3)
		TNT1 A 0 A_SpawnItem("PentagramSpawner")
		TPFG HGFEDCBA 1 Bright
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",10,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",10,0,random(0,360),2,random(20,40))
		TPFG ABCDEFG 1 Bright
		TNT1 A 0 A_NoBlocking
		Stop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR PentagramSpawner
{
Game Doom
XScale 0.25
YScale 0.025
Gravity 1
Radius 1
Height 1
Alpha 1
RenderStyle Add
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+FORCEXYBILLBOARD
+CLIENTSIDEONLY
+NOCLIP
-NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 AAAAA 2 A_SpawnItem("BurningPentagram")
		TNT1 A 20
		TNT1 A 1 A_SpawnItem("PentagramOff")
		TNT1 A 1
		Stop
	}
}

ACTOR BurningPentagram : PentagramSpawner
{
Alpha 0
	States
	{
	Spawn:
		PNTG AAAAAAAAAAAAAAAAAAAAAAA 1 NoDelay A_FadeIn(0.02)
		PNTG A 1 A_FadeOut(0.02)
		Wait
	}
}

ACTOR PentagramOff : PentagramSpawner
{
Renderstyle Normal
	States
	{
	Spawn:
		PNTG B 300
		PNTG B 1 A_FadeOut(0.02)
		Wait
	}
}