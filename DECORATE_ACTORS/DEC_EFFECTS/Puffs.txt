ACTOR HitPuff Replaces BulletPuff
{
Scale 1.5
radius 0
height 0
Mass 500
Gravity 0.01
alpha 0.4
renderstyle Translucent
DamageType Bullet
Decal "BulletDecalNew1"
+NOBLOCKMAP
+NOGRAVITY
+NOEXTREMEDEATH
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
		//Intentional fall-through
	Melee:
		TNT1 A 1
		stop
	}
}

actor ShotPuff : HitPuff
{
DamageType SSG
}

actor ShotgunPuff : HitPuff
{
DamageType Shotgun
}

ACTOR RicoChet
{
Scale 0.09
Speed 0
Radius 1
Height 1
alpha 1.0
renderstyle Add
Obituary "%o ate bullet shrapnel."
-COUNTKILL
-SHOOTABLE
+CLIENTSIDEONLY
+BLOODLESSIMPACT
+FORCEXYBILLBOARD
+THRUACTORS
+NOGRAVITY
	states
	{
	Spawn:
		IPF2 A 0 NoDelay A_SpawnItem("YellowFlareSmall",0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectCeilBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("UnderwaterHitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAA 0 A_CustomMissile("HitSpark",2,0,random(0,360),2,random(30,360))
		TNT1 AA 0 A_CustomMissile("HitSpark22",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_CustomMissile("HitSpark23",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_PlaySound("ricochet/hit")
		TNT1 AAA 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
	Spawn1:
		IPF2 AABB 1 Bright A_SpawnItem("YellowFlareSmall",0)
		Stop
	Spawn2:
		IPF2 CCDD 1 Bright A_SpawnItem("YellowFlareSmall",0)
		Stop
	Spawn3:
		IPF2 EEFF 1 Bright A_SpawnItem("YellowFlareSmall",0)
		Stop
	Spawn4:
		IPF2 GGHH 1 Bright A_SpawnItem("YellowFlareSmall",0)
		Stop
	}
}

ACTOR GiantWaterRicochet : RicoChet
{
XScale 3.0
YScale 2.0
alpha 0.9
renderstyle Add
	states
	{
	Spawn:
		GSPL ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		Stop
	}
}

ACTOR GiantNukageRicochet : GiantWaterRicochet
{
Translation "0:255=%[0,0,0]:[0,1,0]"//Green
}

ACTOR GiantBloodRicochet : GiantWaterRicochet
{
RenderStyle Normal
Translation "0:255=187:191" //Red
}

Actor MachineGunBulletPuff : HitPuff
{
DamageType CutLess
}

Actor RifleBulletPuff : HitPuff		//R30.5 added
{
DamageType Rifle
}

ACTOR SSawPuff : HitPuff
{
scale 0.15
Radius 20
alpha 0.7
Renderstyle Add
damagetype Cut
+NOBLOCKMAP
+NOGRAVITY
+NOEXTREMEDEATH
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		BPUF C 0
	Death:
	XDeath:
	Melee:
		TNT1 A 2
		TNT1 AAAA 0 A_CustomMissile("HitSpark",2,0,random(0,360),2,random(30,360))
		TNT1 AA 0 A_CustomMissile("HitSpark22",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_CustomMissile("HitSpark23",2,0,random(0,360),2,random(30,360))
		BPUF CD 1 Bright
		Stop
	}
}

Actor Tracer : FastProjectile	//R11 ADDED TRACER
{
scale 0.15
damage 0
radius 2
height 2
speed 140
alpha 0.9
renderstyle ADD
Damagetype "Weak"
Projectile
+RANDOMIZE
+FORCEXYBILLBOARD
+DONTSPLASH
+NOEXTREMEDEATH
	states
	{
	Spawn:
		TRAC A 1 Bright
		Loop
	XDeath:
	Death:
		TNT1 A 3
		Stop
	}
}