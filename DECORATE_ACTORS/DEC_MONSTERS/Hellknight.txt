Actor 64HellKnight : HellKnight Replaces HellKnight
{
//$category "Game/BD64_Monsters
Scale 0.8
Height 64
Radius 24
PainChance "SuperFists",220
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"
MeleeRange 60	//default 44
Species HellKnight
BloodType "Brutal_BloodGreen"
BloodColor "00 64 00"	//Hex value
+NOBLOODDECALS
	states
	{
	Spawn:
		BOS2 AB 10 A_Look
		TNT1 A 0 A_SpawnItem("HeadshotTarget20Green",12,60,0)
		Loop
	See:
		TNT1 A 0 A_SpawnItem("HeadshotTarget23Green",12,60,0)
		BOS2 AABBCCDD 3 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:
		TNT1 A 0 A_SpawnItem("HeadshotTarget23Green",12,60,0)
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 A_CustomMeleeAttack(10*random(1,8),"baron/melee")	//Melee and Missile seperated
		Goto See
	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget23Green",0,60,0)
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 Bright A_CustomMissile("64BaronBallGreen",32)	//Melee and Missile seperated
		Goto See
	Pain:
		BOS2 H 2
		BOS2 H 2 A_Pain
		Goto See
	Death:
		BOS2 I 5 A_Scream
		BOS2 J 5 A_NoBlocking
		BOS2 KLM 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOS2 N 35 A_BossDeath
		BOS2 N -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	Raise:
		BOS2 NMLKJI 5
		TNT1 A 0 A_ChangeFlag(THRUACTORS,0)
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	Death.Plasma:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Stop
		TNT1 AAAA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmallGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_SpawnItemEx("BurningFleshEffect2",0,0,5)
		TNT1 A 0 A_SpawnItem("DetectFloorCrater")
		BOSB IJKLMN 4
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOSB N -1 A_BossDeath
		Stop
	Death.SSG:
		TNT1 A 0 A_Recoil(12)
		BOS2 H 1 A_JumpIfCloser(170,"XDeath")
		TNT1 A 0 ThrustThingZ(0,10,0,1)
		Goto Death
	Death.Head:
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("EyesGreen",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAAAA 0 A_CustomMissile("SmallBrainPieceGreen",56,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		BS2H ABCDE 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BS2H F 35 A_BossDeath
		BS2H F -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	Death.Cutless:
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:
	Death.SuperKick:
		TNT1 A 0 A_Jump(170,"Death.Head")
	XDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGreen",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("InstestinGreen",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("XDeathHKARm",52,0,random(0,360),2,random(0,90))
		BOS2 H 1 A_SpawnItemEx("XDeathHKLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_BossDeath
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		KGB1 A 2 ThrustThingZ(0,50,0,0)
		TNT1 A 0 A_ChangeFlag(THRUACTORS,1)
		TNT1 A 0 A_Jump(128,2)
		KGB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		KGB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	LandAfterXDeath1:
		KGB1 B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		KGB1 B -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	LandAfterXDeath2:
		KGB1 D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		KGB1 D -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	}
}

Actor XDeathHKLegs
{
Scale 0.8
Health 0
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+ISMONSTER	//Infrared
+MOVEWITHSECTOR
+CORPSE
	States
	{
	Spawn:
		KGB3 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//Flip horizontal
		KGB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpotGreen",0,0,random(0,360),2,random(0,90))
		KGB3 D -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}

Actor 64BaronBallGreen : BaronBall Replaces BaronBall
{
Scale 0.75
Radius 6
Height 6
RenderStyle Normal
Decal "MedScorch"
	states
	{
	Spawn:
		BAL7 AB 2 NoDelay Bright A_CustomMissile("LensFlareProjectileGreen",-2,0)
		TNT1 A 0 A_CustomMissile("BaronGreenProjectileCore",-2,0)
		Loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItem("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItem("DetectCeilCraterMed")
		BAL7 CDEFGH 1 Bright A_SpawnItem("LensFlareTorchGreen",0,0)
		Stop
	}
}

Actor BaronGreenProjectileCore
{
Scale 0.9
Alpha 0.9
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 J 2 Bright
		Stop
	}
}