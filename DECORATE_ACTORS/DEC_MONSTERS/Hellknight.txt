//r31 added extra deaths
Actor 64HellKnight : HellKnight Replaces HellKnight
{
//Translation "16:31=%[0,0,0]:[0,1,0]","176:191=%[0,0,0]:[0,1,0]","32:47=%[0,0,0]:[0,1,0]"
Scale 0.8
Height 64
Radius 24
PainChance "SuperFists",220
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"		//R31 added
MeleeRange 60//default 44
Species HellKnight
//bloodcolor "18 8c 31"
BloodType "Brutal_BloodGreen"
BloodColor "00 64 00"	//Hex value
+NOBLOODDECALS
	states
	{
	Spawn:		//R31 EXTRA
		BOS2 AB 10 A_Look
		TNT1 A 0 A_SpawnItem("HeadshotTarget30Green",12,60,0)
		TNT1 A 0 A_GiveInventory("GreenArmor",1)		//R19//R20 WAS BlueArmor
		Loop
	See:		//R31 EXTRA
		TNT1 A 0 A_SpawnItem("HeadshotTarget30Green",12,60,0)
		BOS2 AABBCCDD 3 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:		//R31 extra
		TNT1 A 0 A_SpawnItem("HeadshotTarget30Green",12,60,0)
		BOS2 EF 8 A_FaceTarget
//		BOS2 G 8 A_BruisAttack
		BOS2 G 8 A_CustomMeleeAttack(10*random(1,8),"baron/melee")	//R31 Melee and Missile seperated
		Goto See
	Missile:		//R31 extra
		TNT1 A 0 A_SpawnItem("HeadshotTarget30Green",0,60,0)
		BOS2 EF 8 A_FaceTarget
//		BOS2 G 8 Bright A_BruisAttack
		BOS2 G 8 Bright A_CustomMissile("64BaronBallGreen",32)	//R31 Melee and Missile seperated
		Goto See
	Pain:
		BOS2 H 2
		BOS2 H 2 A_Pain
		Goto See
	Pain.Vertigo:
		TNT1 A 0 ThrustThingZ(0,55,0,0)
		BOS2 H 2
		BOS2 H 2 A_Pain
		Goto See
	Death:
		BOS2 I 8
		BOS2 J 8 A_Scream
		BOS2 K 8
		BOS2 L 8 A_NoBlocking
		BOS2 M 8
			TNT1 A 0 A_SpawnItem("GrowingBloodPoolGreen")
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		BOS2 N -1 A_BossDeath
		Stop
	Raise:
		BOS2 NMLKJI 8
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	Death.Plasma:
			TNT1 AAAA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
			TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmallGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)		//R11 WAS "BurningFleshEffect"
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		BOSB IJKLMN 6
			TNT1 A 0 A_SpawnItem("GrowingBloodPoolGreen")									//r31 added
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//r31 removed
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		BOSB N -1 A_BossDeath														//R30.2 added
		Stop
	Death.SSG:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Recoil(12)
		BOS2 H 1 A_JumpIfCloser(170,"XDeath")
		TNT1 A 0 ThrustThingZ(0,10,0,1)
		Goto Death
	Death.Head:	//r31 extra
			TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
			TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
			TNT1 AA 0 A_CustomMissile("EyesGreen",40,0,random(0,360),2,random(-5,5))
			TNT1 AAA 0 A_CustomMissile("SmallBrainPieceGreen",56,0,random(0,360),2,random(0,90))
			TNT1 AAA 0 A_CustomMissile("SmallBrainPieceGreen",56,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		TNT1 A 0 A_NoBlocking
			TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		BS2H ABCDEF 6
			TNT1 A 0 A_SpawnItem("GrowingBloodPoolGreen")
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BS2H F -1 A_BossDeath
		Stop
	Death.Cutless:	//r31 added
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:						//R26.1 added
	Death.SuperKick:						//R26.1 added
		TNT1 A 0 A_Jump(170,"Death.Head")	//R31 extra
	XDeath:
			TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGreen",0,0,random(0,360),2,random(50,130))
			TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
			TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
			TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
			TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
			TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
			TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
			TNT1 AAAA 0 A_CustomMissile("InstestinGreen",32,0,random(0,360),2,random(0,90))
			TNT1 AAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//R31 removed
			TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("XDeathHKARm",52,0,random(0,360),2,random(0,90))
		BOS2 H 1 A_SpawnItemEx("XDeathHKLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	//R30.9 WAS SPAWNITEM
		TNT1 A 0 ThrustThingZ(0,50,0,0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		TNT1 A 0 A_JUMP(128,3)						//R31 added JUMP
		KGB1 A 2 A_ChangeFlag("THRUACTORS",1)
		KGB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")	//R31 removed 25 A FRAMES
		Wait	//R31 WAS Goto LandAfterXDeath
		KGB1 C 2 A_ChangeFlag("THRUACTORS",1)		//R31 added
		KGB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")	//R31 added
		Wait										//R31 added
	LandAfterXDeath1:
		KGB1 B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
			TNT1 A 0 A_SpawnItem("GrowingBloodPoolGreen")
		KGB1 B -1 A_BossDeath						//R30.2 added
		Stop
	LandAfterXDeath2:	//R31 added
		KGB1 D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
			TNT1 A 0 A_SpawnItem("GrowingBloodPoolGreen")
		KGB1 D -1 A_BossDeath
		Stop
	}
}

Actor XDeathHKLegs
{
//Translation "16:31=%[0,0,0]:[0,1,0]","176:191=%[0,0,0]:[0,1,0]","32:47=%[0,0,0]:[0,1,0]"
Scale 0.8
Health 0
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+ISMONSTER		//R30.7 added INFRARED
+MOVEWITHSECTOR	//R31 added
+CORPSE
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)			//R31 FLIP IMAGE HORZ
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//R31 FLIP IMAGE HORZ
		KGB3 ABC 4
			TNT1 A 0 A_CustomMissile("BigBloodSpotGreen",0,0,random(0,360),2,random(0,90))
		KGB3 D -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}

Actor 64BaronBallGreen : BaronBall Replaces BaronBall
{
Scale 0.75
Radius 6
Height 6
RenderStyle Normal
Decal "MedScorch"	//R28.3 added
	states
	{
	Spawn:
		BAL7 AB 2 NoDelay Bright A_CustomMissile("LensFlareProjectileGreen",-2,0)	//R31 added frame B
		TNT1 A 0 A_CustomMissile("BaronGreenProjectileCore",-2,0)
		Loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItem("DetectFloorCraterMed")	//R28.3 added
		TNT1 A 0 A_SpawnItem("DetectCeilCraterMed")		//R28.3 added
		BAL7 CDEFGH 1 Bright A_SpawnItem("LensFlareTorchGreen",0,0)	//r31 fixed frames
		Stop
	}
}

Actor BaronGreenProjectileCore
{
Scale 0.9
Alpha 0.9
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 J 2 Bright		//R30.4 WAS BAL7 G
		Stop
	}
}