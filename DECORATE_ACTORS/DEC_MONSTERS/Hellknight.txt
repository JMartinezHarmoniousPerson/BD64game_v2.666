//r31 added extra deaths
actor 64HellKnight Replaces HellKnight
{
scale 0.8
health 500
height 64
radius 24
speed 8
painchance 50
mass 1000
Painchance "SuperFists",220
Painchance "Kick",255
Painchance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
obituary "%o was splayed by a Hell Knight"
hitobituary "%o was gutted by a Hell Knight"
seesound "knight/sight"
painsound "knight/pain"
deathsound "knight/death"
activesound "knight/active"
DROPITEM "DemonEssenceSpawner"		//R31 ADDED
//bloodcolor "18 8c 31"
MONSTER
-BOSSDEATH
//+FORCEXYBILLBOARD																//R12.3
+FLOORCLIP
	states
	{
	Spawn:		//R31 EXTRA
		TNT1 A 0 NoDelay A_SpawnItem("HeadshotTarget30",25,60,0)
		TNT1 A 0 A_GiveInventory("GreenArmor",1)								//R19//R20 WAS BlueArmor
		BOS2 A 10 A_Look
		loop
	See:		//R31 EXTRA
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,60,0)
		BOS2 AABBCCDD 3 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:
	Missile:		//R31 EXTRA
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,60,0)
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 A_BruisAttack
		goto See
	Pain:
		BOS2 H 2
		BOS2 H 2 A_Pain
		goto See
	Pain.Vertigo:
		TNT1 A 0 ThrustThingZ(0,55,0,0)
		BOS2 H 2
		BOS2 H 2 A_Pain
		Goto See
	Death:
		BOS2 I 8
		BOS2 J 8 A_Scream
		BOS2 K 8
		BOS2 L 8 A_NoBlocking
		BOS2 M 8
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BOS2 N -1 A_BossDeath
		stop
	Raise:
		BOS2 NMLKJI 8
		goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	Death.Plasma:
		TNT1 AAAA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmall",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)					//R11 WAS "BurningFleshEffect"
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		POL5 A 0 A_PlaySound("Misc/Gibbed")
		BOSB IJKLMN 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")									//r31 added
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//r31 removed
		BOSB N -1 A_BossDeath														//R30.2 ADDED
		Stop
	Death.SSG:
		BOS2 H 1
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Recoil(12)
		TNT1 A 0 A_JumpIfCloser(170,"XDeath")
		BOS2 H 1
		TNT1 A 0 ThrustThingZ(0,10,0,1)
		Goto Death
	Death.Head:	//r31 extra
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("Brains3",40,0,random(0,360),2,random(-5,5))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		POL5 A 0 A_PlaySound("Misc/Gibbed")
		tnt1 a 0 A_NoBlocking
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		BS2H ABCDEF 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BS2H F -1 A_BossDeath
		Stop
	Death.Cutless:	//r31 added
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:															//R26.1
	Death.SuperKick:															//R26.1
		TNT1 A 0 A_Jump(170,"Death.Head")		//R31 extra
	XDeath:
		BOS2 H 1
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("Instestin",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//R31 DISABLED
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBig",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("XDeathHKARm",52,0,random(0,360),2,random(0,90))
		TNT1 A 1 A_SpawnItemEx("XDeathHKLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)//R30.9 WAS SPAWNITEM
		TNT1 A 0 ThrustThingZ(0,50,0,0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_JUMP(128,3)						//R31 NEW JUMP
		KGB1 A 2 A_ChangeFlag("THRUACTORS",1)
		KGB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")	//R31 REMOVED 25 A FRAMES
		WAIT	//R31 WAS Goto LandAfterXDeath
		KGB1 C 2 A_ChangeFlag("THRUACTORS",1)		//R31 ADDED
		KGB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")	//R31 ADDED
		WAIT										//R31 ADDED
	LandAfterXDeath1:
		KGB1 B 1 A_stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		KGB1 B -1 A_BossDeath						//R30.2 ADDED
		Stop
	LandAfterXDeath2:	//R31 NEW
		KGB1 D 1 A_stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		KGB1 D -1 A_BossDeath
		Stop
	}
}

Actor XDeathHKLegs
{
Scale 0.8
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+ISMONSTER																		//R30.7 ADDED INFRARED
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JUMP(128,2)				//R31 FLIP IMAGE HORZ
		TNT1 A 0 A_SETSCALE(0-ScaleX,ScaleY)		//R31 FLIP IMAGE HORZ
		KGB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))
		KGB3 D -1
		Stop
	}
}

actor 64BaronBallGreen : BaronBall Replaces BaronBall
{
scale 0.75
renderstyle Add
Decal "MedScorch"																//R28.3
	states
	{
	Spawn:
		BAL7 A 1 BRIGHT A_SpawnItem("LensFlareProjectileGreen")
		TNT1 AA 0 A_SpawnItem("BaronGreenProjectileCore")
		loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)				//R28.3 ADDED
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)				//R28.3 ADDED
		BAL1 DEFGHIJ 1 BRIGHT A_SpawnItem("LensFlareTorchGreen",0,0)
		stop
	}
}

Actor BaronGreenProjectileCore
{
Scale 0.9
Alpha 0.9
Renderstyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 J 2 BRIGHT															//R30.4 WAS BAL7 G
		Stop
	}
}