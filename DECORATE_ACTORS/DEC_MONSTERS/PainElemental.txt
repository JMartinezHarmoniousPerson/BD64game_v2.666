Actor 64PainElemental : PainElemental Replaces PainElemental
{
//$category "Game/BD64_Monsters
Scale 0.8
Speed 6
PainChance "SuperFists",255
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
HitObituary "%o got too close to a Pain Elemental."
DropItem "DemonEssenceSpawner"
+FORCEXYBILLBOARD
+NOBLOODDECALS
	states
	{
	Spawn:
		PAIN AD 10 A_LookEx
		Loop
	See:
		PAIN AAAAAA 2 A_Chase("","")
		PAIN A 2 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Missile:
		PAIN BB 6 A_FaceTarget
		PAIN C 6 Bright A_FaceTarget
		TNT1 A 0 A_JumpIf(CallACS("D64MonstersOnlyReplacer")==1,2)
		TNT1 A 0 A_JumpIf(CallACS("PainStalkerRandomACS")==1,4)
		TNT1 A 0 A_DualPainAttack("64LostSoul")
		PAIN CB 3 A_FaceTarget
		Goto See
		TNT1 A 0 A_DualPainAttack
		PAIN CB 3 A_FaceTarget
		Goto See
	Pain:
		PAIN D 6
		PAIN D 6 A_Pain
		Goto See
	Death:
		PAIN E 4 Bright
		PAIN F 4 Bright A_Scream
		PAIN GH 4 Bright
		TNT1 A 0 A_JumpIf(CallACS("D64MonstersOnlyReplacer")==1,2)
		TNT1 A 0 A_JumpIf(CallACS("PainStalkerRandomACS")==1,3)
		PAIN I 4 Bright A_PainDie("64LostSoul")
		TNT1 A 0 A_Jump(256,2)
		PAIN I 4 Bright A_PainDie
		TNT1 A 0 A_SpawnItem("64BarrelExplosion")
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BurningXDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistExtraBig",45,0,random(0,360),2,random(10,90))
		TNT1 AAAAAA 0 A_CustomMissile("SuperGoreSpawner",45,0,random(0,360),2,random(10,90))
		PAIN JKL 4 Bright
		Stop
	Raise:
		PAIN LKJIHGFE 8
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}

Actor 64NightmareElemental : 64PainElemental 19126
{
//$category "Game/BD64_Monsters
Speed 12
HitObituary "%o was tormented by an Agony Elemental."
Tag "Agony Elemental"
Alpha 0.5
RenderStyle Translucent
PainChance 64
Health 600
+MissileMore
	states
	{
	Spawn:
		NPEL AD 10 A_LookEx
		Loop
	See:
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 A 0 A_SpawnItem("NightmarePainSprite1")
		TNT1 A 1 A_Wander
		TNT1 AAAA 3 A_chase
		Loop
	Missile:
		NPEL BB 7 A_FaceTarget
		NPEL C 7 Bright A_FaceTarget
		TNT1 A 0 A_JumpIf(CallACS("D64MonstersOnlyReplacer")==1,2)
		TNT1 A 0 A_JumpIf(CallACS("PainStalkerRandomACS")==1,5)
		TNT1 A 0 A_PainAttack("64LostSoul")
		TNT1 A 0 A_DualPainAttack("64LostSoul")
		NPEL CB 6 A_FaceTarget
		Goto See
		TNT1 A 0 A_PainAttack
		TNT1 A 0 A_DualPainAttack
		NPEL CB 6 A_FaceTarget
		Goto See
	Pain:
		NPEL D 6
		NPEL D 6 A_Pain
		Goto See
	Death:
		NPEL E 4 Bright
		NPEL F 4 Bright A_Scream
		NPEL GH 4 Bright
		TNT1 A 0 A_JumpIf(CallACS("D64MonstersOnlyReplacer")==1,2)
		TNT1 A 0 A_JumpIf(CallACS("PainStalkerRandomACS")==1,3)
		NPEL I 4 Bright A_PainDie("64LostSoul")
		TNT1 A 0 A_Jump(256,2)
		NPEL I 4 Bright A_PainDie
		TNT1 A 0 A_SpawnItem("64BarrelExplosion")
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BurningXDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistExtraBig",45,0,random(0,360),2,random(10,90))
		TNT1 AAAAAA 0 A_CustomMissile("SuperGoreSpawner",45,0,random(0,360),2,random(10,90))
		NPEL JKL 4 Bright
		Stop
	Raise:
		NPEL LKJIHGFE 8
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}

Actor NightmarePainSprite1
{
Scale 0.75
Alpha 0.08
RenderStyle Translucent
+NOBLOCKMAP
+CLIENTSIDEONLY
+ISMONSTER		//Infrared
	States
	{
	Spawn:
		NPEL AAAA 1 A_FadeIn(0.09)
		NPEL A 4
		NPEL AAAAAA 1 A_FadeOut(0.09)
		Stop
	}
}