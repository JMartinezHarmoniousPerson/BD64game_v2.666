ACTOR 64Hellstorm 19132
{
//$category "Game/BD64_Monsters
Scale 0.8
Health 800
Radius 20
Height 56
Mass 500
Speed 20
Alpha 0.6
RenderStyle Translucent
MeleeRange 150
PainChance 10
PainChance "SuperFists",200
PainChance "Kick",200
PainChance "SuperKick",200
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
obituary "%o was torn apart by a Hellstorm"
Tag "Hellstorm"
seesound "tornado/see"
painsound "tornado/pain"
deathsound "tornado/death"
activesound "tornado/active"
DropItem "DemonEssenceSpawner"
BloodType "Brutal_BloodGreen"
BloodColor "00 64 00"	//Hex value
MONSTER
+FLOORCLIP
+NOTARGET
+NOBLOODDECALS
+QUICKTORETALIATE
+NORADIUSDMG
	states
	{
	Spawn:
		S1R3 AB 10 A_LookEx
		Loop
	See:
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite1")
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite2")
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItem("HellstormSprite3")
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite4")
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite1")
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItem("HellstormSprite2")
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite3")
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("HellstormSprite4")
		TNT1 A 1 A_chase
		TNT1 AAAAAAAA 3 A_chase
		Loop
	Missile:
		S1R3 F 8 BRIGHT A_FaceTarget
		S1R3 G 8 BRIGHT A_FaceTarget
		S1R3 H 8 BRIGHT A_FaceTarget
		S1R3 K 4 BRIGHT A_CustomMissile("64HellstormSoulBall1",58,12,90,0)
		S1R3 K 4 BRIGHT A_CustomMissile("64HellstormSoulBall2",58,-12,-90,0)
		S1R3 L 4 BRIGHT A_CustomMissile("64HellstormFire",0,0,10,2,0)
		S1R3 L 4 BRIGHT A_CustomMissile("64HellstormFire",0,0,-10,2,0)
		S1R3 M 20 BRIGHT
		Goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		SP1N ABCDEFGHABCDEFGHABCDEFGHABCDEFGH 1 BRIGHT
		TNT1 A 0 A_PlaySound("tornado/form")
		ZP1N ABC 1 BRIGHT
	TornadoSpinning:
		TNT1 A 0 A_JumpIfInventory("HellstormTornadoStopper",30,"StopSpin")
		TNT1 A 0 A_PlaySound("tornado/form")
		TNT1 A 0 A_Explode(8,180,XF_NOTMISSILE)
		TNT1 AA 0 A_CustomMissile("ExplosionSmokeFast22",30,0,random(0,360),2,random(0,360))
		TNT1 AAAA 0 A_CustomMissile("64LeavesEffect",Random(16,64),Random(-32,32),Random(0,360),0,0)
		TNT1 A 0 A_SpawnItem("HellstormTornadoSprite")
		TNT1 A 0 A_GiveInventory("HellstormTornadoStopper",1)
		ZP1N ABC 1 BRIGHT A_Chase("","")
		Loop
	StopSpin:
		TNT1 A 0 A_TakeInventory("HellstormTornadoStopper",30)
		SP1N ABCDEFGHABCDEFGHABCDEFGHABCDEFGH 1 BRIGHT
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Pain:
		S1R3 Q 5
		S1R3 Q 5 A_Pain
		Goto See
	Death:
		TNT1 AA 0 A_CustomMissile("Brutal_BloodGreen",0,0,random(0,360),2,random(50,130))
		S1R3 R 7
		TNT1 AAA 0 A_CustomMissile("Brutal_BloodGreen",0,0,random(0,360),2,random(50,130))
		S1R3 S 7 A_Scream
		TNT1 AAAA 0 A_CustomMissile("Brutal_BloodGreen",0,0,random(0,360),2,random(50,130))
		S1R3 T 7 A_NoBlocking
		TNT1 A 0 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("CeilBloodLaunchergreen",0,0,random(0,360),2,random(50,130))
		TNT1 AAA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AAA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AAA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAAA 0 A_CustomMissile("IntestineGreen",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		S1R3 UVW 7
		S1R3 X 5
		Stop
	}
}

ACTOR 64LeavesEffect
{
Scale 0.5
Alpha 1
BounceType "Doom"
RenderStyle Translucent
Radius 8
Height 16
Speed 4
Damage 1
PROJECTILE
+RANDOMIZE
+NOGRAVITY
+Float
	states
	{
	Spawn:
		L3F9 ABCDEFGHIJKL 1 BRIGHT A_CustomMissile("64LeavesTrail",0,0,random(-18,18),2,0)
		L3F9 ABCDEFGHIJKL 1 BRIGHT A_CustomMissile("64LeavesTrail",0,0,random(-18,18),2,0)
		L3F9 ABCDEFGHIJKL 1 BRIGHT A_FadeOut(0.05)
		Stop
	Death:
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		L3F9 ABCDEFGHIJKL 1 BRIGHT A_FadeOut(0.05)
		Stop
	}
}

Actor 64LeavesTrail
{
Scale 0.5
Alpha 0.6
RenderStyle Translucent
Mass 1
+NOBLOCKMAP
+NOGRAVITY
+FLOATBOB
+CLIENTSIDEONLY
	States
	{
	Spawn:
		L3F9 ABCDEFGHIJKL 1 A_FadeOut(0.05)
		Stop
	}
}

Actor 64HellstormSoulBall1 : 64RevenantTracer3
{
Damage 8
Speed 10
Scale 0.7
DamageType "BFG"
PROJECTILE
	States
	{
	Spawn:
		ZT63 A 2 NoDelay Bright A_SpawnItem("LensFlareProjectileBlue")
		TNT1 A 0 A_SeekerMissile(15,15)
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		ZT63 B 2 Bright A_SpawnItem("LensFlareProjectileBlue")
		TNT1 A 0 A_SeekerMissile(15,15)
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		Loop
	Death:
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionParticleBlueHeavySmall",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleBlueHeavySmall",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionParticleBlueVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_playSound("EXPLOSIO3",2)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 2
		TNT1 A 0 A_Explode(5,64)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		ZT63 CDEFGH 2 Bright
		Stop
	}
}

ACTOR 64HellstormSoulBall2 : 64HellstormSoulBall1
{
Speed 9
}

Actor 64HellstormFire : MotherFire
{
	States
	{
	Spawn:
		ZFYR ABCDE 4 Bright A_CustomMissile("64HellstormFireTrail",0,0,0,2,0)
		TNT1 A 0 A_FireCrackle
		Loop
	Death:
		ZFYR E 1 Bright
		Stop
	}
}

Actor 64HellstormFireTrail : MotherFireTrail
{
renderstyle stencil
stencilcolor "88 80 BF"
	States
	{
	Spawn:
		MPUF A 5 Bright
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_ChangeFlag("DONTSPLASH",1)
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		MPUF A 5 Bright A_CustomMissile("LongBlueFlameParticle1",random(0,8),0,random(0,360),2,random(80,100))
		TNT1 A 0 A_Explode(15,35,1)
		TNT1 A 1 A_SpawnItemEx("DetectFloorCraterSmallMotherdemon")
		Stop
	}
}

Actor LongBlueFlameParticle1 : LongFlameParticle1
{
	States
	{
	Spawn:
		FZR5 IJKLMNOP 1 Bright
		Stop
	}
}

Actor HellstormSprite1
{
Scale 0.8
Alpha 0.2
RenderStyle Translucent
+NOBLOCKMAP
+CLIENTSIDEONLY
+ISMONSTER	//Infrared
	States
	{
	Spawn:
		S1R3 AAAA 1 A_FadeIn(0.05)
		S1R3 A 4
		S1R3 AAAAAA 1 A_FadeOut(0.05)
		Stop
	}
}

Actor HellstormSprite2 : HellstormSprite1
{
	States
	{
	Spawn:
		S1R3 BBBB 1 A_FadeIn(0.05)
		S1R3 B 4
		S1R3 BBBBBB 1 A_FadeOut(0.05)
		Stop
	}
}

Actor HellstormSprite3 : HellstormSprite1
{
	States
	{
	Spawn:
		S1R3 CCCC 1 A_FadeIn(0.05)
		S1R3 C 4
		S1R3 CCCCC 1 A_FadeOut(0.05)
		Stop
	}
}

Actor HellstormSprite4 : HellstormSprite1
{
	States
	{
	Spawn:
		S1R3 DDDD 1 A_FadeIn(0.05)
		S1R3 D 4
		S1R3 DDDDDD 1 A_FadeOut(0.05)
		Stop
	}
}

Actor HellstormTornadoSprite : HellstormSprite1
{
	States
	{
	Spawn:
		ZP1N ABCABC 1 A_FadeIn(0.05)
		ZP1N ABCABC 1
		ZP1N ABCABC 1 A_FadeOut(0.05)
		Stop
	}
}

ACTOR HellstormTornadoStopper : Inventory
{
Inventory.MaxAmount 30
Inventory.Amount 1
}