Actor 64Knightmare 19118
{
Spawnid 178
Scale 0.8
Health 500
Height 64
Radius 24
Mass 500
Speed 14
PainChance 75
Alpha 0.6
RenderStyle Translucent
DamageFactor "Head",0.8
Obituary "%o was charred by a Knightmare."
HitObituary "%o was mangled by a Knightmare."
SeeSound "knight/sight"
PainSound "knight/pain"
DeathSound "knight/death"
ActiveSound "knight/active"
Tag "Knightmare"
GibHealth 15
PainChance "SuperFists",255
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DamageFactor "chainsaw",0.5
DropItem "DemonEssenceSpawner"
MONSTER
+BOSSDEATH
+FLOORCLIP
	states
	{
	Spawn:
		BOZ3 AB 10 A_Look
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,60,0)
		TNT1 A 0 A_GiveInventory("GreenArmor",1)
		Loop
	See:
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite1")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite2")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItem("KnightmareSprite3")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItem("KnightmareSprite4")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite1")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite2")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite3")
		TNT1 A 0 A_SpawnItem("HeadshotTarget4",25,60,0)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItem("KnightmareSprite4")
		TNT1 A 0 A_SpawnItem("HeadshotTarget20",25,60,0)
		TNT1 A 1 A_chase
		TNT1 AAAA 3 A_chase
		Loop
	Melee:
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,60,0)
		BOZ3 EF 6 A_FaceTarget
		BOZ3 G 6 A_CustomMeleeAttack(10*random(2,9),"baron/melee")
		Goto See
	Missile:
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,60,0)
		BOZ3 EF 6 A_FaceTarget
		BOZ3 G 6 Bright A_CustomMissile("64BaronBallYellow",32)
		Goto See
	Pain:
		BOZ3 H 2
		BOZ3 H 2 A_Pain
		Goto See
	Pain.Vertigo:
		TNT1 A 0 ThrustThingZ(0,55,0,0)
		BOZ3 H 2
		BOZ3 H 2 A_Pain
		Goto See
	Death:
		BOZ3 I 8
		BOZ3 J 8 A_Scream
		BOZ3 K 8
		BOZ3 L 8 A_NoBlocking
		BOZ3 M 8
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		BOZ3 N -1 A_BossDeath
		Stop
	Raise:
		BOZ3 NMLKJI 8
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	Death.Plasma:
		TNT1 AAAA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmall",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		BOSB IJKLMN 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		BOSB N -1 A_BossDeath
		Stop
	Death.SSG:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Recoil(12)
		BOZ3 H 1 A_JumpIfCloser(170,"XDeath")
		TNT1 A 0 ThrustThingZ(0,10,0,1)
		Goto Death
	Death.Head:
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("Brains3",40,0,random(0,360),2,random(-5,5))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		TNT1 A 0 A_NoBlocking
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		BZ1H ABCDEF 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		BZ1H F -1 A_BossDeath
		Stop
	Death.Cutless:
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:
	Death.SuperKick:
		TNT1 A 0 A_Jump(170,"Death.Head")
	XDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("Instestin",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBig",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("KnightmareArm",52,0,random(0,360),2,random(0,90))
		BOZ3 H 1 A_SpawnItemEx("KnightmareLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 ThrustThingZ(0,50,0,0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_KillMaster("None")	//08-08-22 tag 666 fix
		TNT1 A 0 A_JUMP(128,3)
		BZB1 A 2 A_ChangeFlag("THRUACTORS",1)
		BZB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		BZB1 C 2 A_ChangeFlag("THRUACTORS",1)
		BZB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	LandAfterXDeath1:
		BZB1 B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BZB1 B -1 A_BossDeath
		Stop
	LandAfterXDeath2:
		BZB1 D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BZB1 D -1 A_BossDeath
		Stop
	}
}

Actor KnightmareLegs
{
Scale 0.8
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+ISMONSTER
+MOVEWITHSECTOR
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)
		BZB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))
		BZB3 D -1
		Stop
	}
}

ACTOR KnightmareArm
{
Radius 8
Height 8
Mass 1
SeeSound "misc/xdeath4"
DeathSound "misc/xdeath3"
Decal BrutalBloodSuper
damagetype Blood
BounceCount 1
+BOUNCEONFLOORS
+NOBLOCKMAP
+MISSILE
+NOTELEPORT
+MOVEWITHSECTOR
+RIPPER
+BLOODLESSIMPACT
-DONTSPLASH
+THRUGHOST
+THRUACTORS
+FLOORCLIP
+ISMONSTER
Scale 0.8
Speed 3
BounceFactor 0.01
+DOOMBOUNCE
+CLIENTSIDEONLY
+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JUMP(128,2)
		TNT1 A 0 A_SETSCALE(0-ScaleX,ScaleY)
		BZB2 A 4 A_CHECKFLOOR("DEATH")
		WAIT
	Death:
		BZB2 B 1
		TNT1 A 0 A_SpawnItem("Brutal_BloodSpot",0,0,0,1)
		BZB2 B -1
		Stop
	}
}

Actor KnightmareSprite1
{
Scale 0.75
Alpha 0.08
RenderStyle Translucent
+NOBLOCKMAP
+CLIENTSIDEONLY
+ISMONSTER
	States
	{
	Spawn:
		BOZ3 AAAA 1 A_FadeIn(0.09)
		BOZ3 A 4
		BOZ3 AAAAAA 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite2 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 BBBB 1 A_FadeIn(0.09)
		BOZ3 B 4
		BOZ3 BBBBBB 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite3 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 CCCC 1 A_FadeIn(0.09)
		BOZ3 C 4
		BOZ3 CCCCC 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite4 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 DDDD 1 A_FadeIn(0.09)
		BOZ3 D 4
		BOZ3 DDDDDD 1 A_FadeOut(0.09)
		Stop
	}
}

Actor 64BaronBallYellow : BaronBall
{
Scale 0.75
Speed 20
FastSpeed 25
Damage 12
RenderStyle Normal
Decal "MedScorch"
Translation "0:255=%[0,0,0]:[1,1,0]"
	states
	{
	Spawn:
		BAL7 AB 2 NoDelay Bright A_CustomMissile("LensFlareProjectileYellow",-2,0)
		TNT1 A 0 A_CustomMissile("BaronYellowProjectileCore",-2,0)
		Loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleYellowHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleYellowHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleYellowVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleYellow",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		BAL7 CDEFGH 1 Bright A_SpawnItem("LensFlareTorchYellow",0,0)
		Stop
	}
}

Actor BaronYellowProjectileCore
{
Translation "0:255=%[0,0,0]:[1,1,0]"
Scale 0.9
Alpha 0.9
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 J 2 Bright
		Stop
	}
}