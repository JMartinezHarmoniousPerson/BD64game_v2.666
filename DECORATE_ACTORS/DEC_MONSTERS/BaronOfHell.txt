//R31 added extra deaths
actor 64BaronOfHell : BaronOfHell Replaces BaronOfHell
{
scale 0.9
missiletype BaronBall2
Painchance "Provoke",255
Painchance "SuperFists",200
Painchance "Kick",255
Painchance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DROPITEM "DemonEssenceSpawner"		//R31 added
//bloodcolor "8C 39 10"
	states
	{
	Spawn:		//R31 extra
		BOSS AB 10 A_Look
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,70,0)
		TNT1 A 0 A_GiveInventory("GreenArmor",1)			//R19 added //R20 was BlueArmor
		Loop
	See:		//R31 extra
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,70,0)
		BOSS AABBCCDD 2 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:		//R31 extra
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,70,0)
		TNT1 A 0 A_JumpIfInTargetInventory("IsDeadPLayer",1,"Stomp")
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_BasicAttack(10,"baron/melee","64BaronBall",32)
		goto See
	Missile:	//R31 extra
		TNT1 A 0 A_SpawnItem("HeadshotTarget30",25,70,0)
		TNT1 A 0 A_JumpIfInTargetInventory("IsDeadPLayer",1,"See")
		BOSS EF 8 A_FaceTarget
		BOSS G 8 Bright A_BasicAttack(10,"baron/melee","64BaronBall",32)
		goto See
	Pain.Provoke:
		BOSS A 2
		Goto See
	Pain.Vertigo:
		TNT1 A 0 ThrustThingZ(0,55,0,0)
		BOSS H 2
		BOSS H 2 A_Pain
		Goto See
	Death:
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_NoBlocking
		BOSS M 8
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")	//R31 added
			TNT1 A 0 A_KillMaster("None")//,KILS_FOILINVUL)	//08-08-22 tag 666 fix
		BOSS N -1 A_BossDeath
		stop
	Raise:
		BOSS NMLKJI 8
		goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	Stomp:
		//BOSS A 1 A_FaceTarget
		//BOSS ABCD 4 A_Recoil(-1)
		BRST AAAAAABBCD 3 A_faceTarget
		BRST DDEF 3 A_FaceTarget
		TNT1 A 0 A_BasicAttack(40,"None","64BaronBall",32)
		BRST G 4
		Goto See
	Death.SSG:
		BOSS H 1
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Recoil(12)
		TNT1 A 0 A_JumpIfCloser(170,"XDeath")
		BOSS H 1
		TNT1 A 0 ThrustThingZ(0,10,0,1)
		Goto Death
	Death.Head:		//R31 extra
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("Brains3",40,0,random(0,360),2,random(-5,5))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		TNT1 AAA 0 A_CustomMissile("SmallBrainPiece",56,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		tnt1 a 0 A_NoBlocking
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		BS1H ABCDEF 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
			TNT1 A 0 A_KillMaster("None")//,KILS_FOILINVUL)	//08-08-22 tag 666 fix
		BS1H F -1 A_BossDeath
		Stop
	Death.Cutless:	//R31 extra
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:						//R26.1 added
	Death.SuperKick:						//R26.1 added
		TNT1 A 0 A_Jump(170,"Death.Head")	//R31 extra
	XDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",52,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",52,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1",50,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2",50,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("Instestin",52,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",55,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBig",45,0,random(0,360),2,random(10,90))
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//R31 removed
		TNT1 A 0 A_CustomMissile("XDeathBaronARm",52,0,random(0,360),2,random(0,90))
		TNT1 A 1 A_SpawnItemEx("XDeathBaronLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	//R30.9 was SpawnItem
		TNT1 A 0 ThrustThingZ(0,50,0,0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
			TNT1 A 0 A_KillMaster("None")//,KILS_FOILINVUL)	//08-08-22 tag 666 fix
		TNT1 A 0 A_JUMP(128,3)						//R31 added jump
		BGB1 A 2 A_ChangeFlag("THRUACTORS",1)
		BGB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")	//R31 removed 25 A frames
		WAIT	//R31 was Goto LandAfterXDeath
		BGB1 C 2 A_ChangeFlag("THRUACTORS",1)		//R31 added
		BGB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")	//R31 added
		WAIT										//R31 added
	LandAfterXDeath1:
		BGB1 B 1 A_stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BGB1 B -1 A_BossDeath	//R30.2 added
		Stop
	LandAfterXDeath2:	//R31 added
		BGB1 D 1 A_stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")
		BGB1 D -1 A_BossDeath
		Stop
	Death.Plasma:		//R31 extra
		TNT1 AAAA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmall",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)		//R11 was "BurningFleshEffect"
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		BOSB IJKLMN 6
		TNT1 A 0 A_SpawnItem("GrowingBloodPool")		//r31 added
//		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))	//r31 removed
			TNT1 A 0 A_KillMaster("None")//,KILS_FOILINVUL)	//08-08-22 tag 666 fix
		BOSB N -1 A_BossDeath
		Stop
	}
}

Actor XDeathBaronLegs
{
XScale -0.9
YScale 0.9
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+ISMONSTER	//R30.7 added for infrared
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JUMP(128,2)			//R31 flip image Horz
		TNT1 A 0 A_SETSCALE(0-ScaleX,ScaleY)	//R31 flip image Horz
		BGB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpot",0,0,random(0,360),2,random(0,90))
		BGB3 D -1
		Stop
	}
}

actor 64BaronBall : BaronBall
{
scale 0.8
renderstyle Normal
Decal "MedScorch"	//R28.3 added
	states
	{
	Spawn:
		BRNR AB 2 NoDelay BRIGHT A_CustomMissile("LensFlareProjectileOrange",-2,0)	//R31 added frame B
		TNT1 A 0 A_CustomMissile("BaronProjectileCore",-2,0)
		loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(20,40))		//R11 unused //R31 was "ExplosionParticleFast"
		TNT1 AAAA 0 A_CustomMissile("SmallFireballFireTrail",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)			//R28.3 added
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)			//R28.3 added
		BRNR CDEFGH 1 BRIGHT A_SpawnItem("LensFlareTorchOrange",0,0)	//R31 fixed frames
		stop
	}
}

Actor BaronProjectileCore
{
Scale 0.9
Alpha 0.9
Renderstyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 I 2 BRIGHT
		Stop
	}
}