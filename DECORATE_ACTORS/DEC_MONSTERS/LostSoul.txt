actor 64LostSoul : LostSoul Replaces LostSoul
{
scale 0.8
health 50
Alpha 0.99
renderstyle Normal
Painchance "SuperFists",200
Painchance "Kick",255
Painchance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
+NOBLOCKMONST
+NOBLOOD
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		SKUL ABC 1 A_Look
		TNT1 A 0 A_GiveInventory("GreenArmor",1)								//R19//R20 was BlueArmor
		loop
	See:
		SKUL A 1 BRIGHT A_Chase
		SKUL A 1 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL B 1 BRIGHT A_Chase("","")
		SKUL B 1 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL B 1 BRIGHT A_Chase
		SKUL C 1 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Missile:
		SKUL D 2 BRIGHT A_FaceTarget
		SKUL E 2 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL D 2 BRIGHT A_FaceTarget
		SKUL E 2 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL D 2 BRIGHT A_FaceTarget
		SKUL E 2 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL D 2 BRIGHT A_FaceTarget
		SKUL E 2 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL E 1 BRIGHT A_SkullAttack(30)
		TNT1 AAAAA 0
		SKUL EE 1 BRIGHT A_SpawnItemEx("LostSoulTrail",0,0,30)
		SKUL D 2 BRIGHT A_SpawnItemEx("YellowFlare",0,0,30)
		goto Missile+10
	Pain:
		SKUL FF 2 A_SpawnItemEx("YellowFlare",0,0,30)
		SKUL F 1 A_Pain
		goto See
	Pain.Vertigo:
		TNT1 A 0 BRIGHT ThrustThingZ(0,55,0,0)
		SKUL F 3 BRIGHT
		SKUL F 3 BRIGHT A_Pain
		Goto See
	Death:
		SKUL G 2 bright
		TNT1 A 0 A_PlaySound("LSOD",1)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAA 0 A_CustomMissile("BarrelExplosionFire",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",30,0,random(0,360),2,random(0,360))
		SKUL H 4 bright A_Scream
		SKUL I 4 bright
		SKUL J 4 A_NoBlocking
		SKUL KLMNOP 4 A_FadeOut(0.1667)
		stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}

actor LostSoulTrail
{
scale 1.0
Alpha 0.9
Renderstyle Add
+FORCEXYBILLBOARD
+NOINTERACTION
+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 1
		MTB2 ABCABC 1 BRIGHT
		Stop
	}
}