Actor 64ForgottenOne : LostSoul
{
Scale 0.8
Health 70
Alpha 0.99
Speed 16
PainChance 128
AttackSound "alphasoul/melee"
PainSound "alphasoul/pain"
DeathSound "alphasoul/death"
ActiveSound "alphasoul/active"
Obituary "%o was electrocuted by a Forgotten One."
RenderStyle Normal
PainChance "SuperFists",200
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
+NOBLOOD
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		ESKU AAA 2 A_Look
		Loop
	See:
		ESKU AAAA 2 A_Chase("Melee","Missile",CHF_FASTCHASE)
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:
		TNT1 A 0 ThrustThingZ(0,15,0,0)
		ESKU AA 4 A_Recoil(4)
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		ESKU B 4 A_FaceTarget
		TNT1 A 0 A_VileTarget("64AlphaShock")
		ESKU CBCBCDCD 4 A_FaceTarget
		TNT1 A 0 A_VileAttack("alphasoul/melee",15,0,0,0,"AlphaLSAtk")
		ESKU E 4 A_FaceTarget
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Pain:
		ESKU FF 2
		ESKU F 1 A_Pain
		goto See
	Pain.Vertigo:
		TNT1 A 0 ThrustThingZ(0,55,0,0)
		ESKU F 3
		ESKU F 3 A_Pain
		Goto See
	Death:
		ESKU G 2 Bright
		TNT1 A 0 A_PlaySound("alphasoul/death",1)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAA 0 A_CustomMissile("BarrelExplosionFire",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",30,0,random(0,360),2,random(0,360))
		ESKU H 4 Bright A_Scream
		ESKU I 4 Bright
		ESKU J 4 A_NoBlocking
		ESKU KLMN 4 A_FadeOut(0.1667)
		ESKU O 4 A_FadeOut(0.1667)
		Wait
	}
}

ACTOR 64AlphaShock : ArchvileFire
{
Alpha 0.75
RenderStyle Add
+NOBLOCKMAP
+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 4 Bright A_StartFire
		SH9K ABCDEF 4 Bright A_Fire
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleYellowVeryFast",20,0,random(0,360),2,random(0,180))
		TNT1 A 0 A_PlaySound("LSSTART",3)
		SH9K FEDCB 2 A_FadeOut(0.1667)
		SH9K A 2 A_FadeOut(0.1667)
		Wait
	}
}