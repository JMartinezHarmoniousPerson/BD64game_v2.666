ACTOR PistolSpawner : CustomInventory Replaces Pistol
{
//$category "Game/BD64_Weapons
Scale 0.82
Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Pistol",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PistolTactical",1)
		Stop
	}
}

Actor 64Pistol : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 1900
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Weapon.AmmoType "Clip"
Obituary "$OB_MPPISTOL"
+WEAPON.WIMPY_WEAPON
+WEAPON.NOAUTOFIRE
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
Weapon.SlotNumber 2
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	states
	{
	Ready:
		PISG A 1 A_WeaponReady
		Wait
	Select:
		PISG A 1 A_Raise
		wait
	Deselect:
		PISG A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_PlaySound("PISTOLF",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.5, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 Bright
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		PISF D 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		PISG A 7 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		PIST A -1
		Stop
	}
}

ACTOR PistolAmmo : Ammo
{
Inventory.icon "PISTA0"
Ammo.BackPackAmount 0
Ammo.BackpackMaxAmount 10
Inventory.Amount 0
Inventory.MaxAmount 10
+IGNORESKILL
}

Actor IsSilencer : Inventory
{
inventory.maxamount 1
}

Actor FirstTimePistol : Inventory
{
inventory.maxamount 1
}

Actor 64PistolTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.SelectionOrder 1901
Weapon.AmmoUse1 1
Weapon.AmmoGive1 10
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Weapon.AmmoType2 "PistolAmmo"
Obituary "$OB_MPPISTOL"
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
Weapon.SlotNumber 2
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
+WEAPON.AMMO_CHECKBOTH
	states
	{
	Spawn:
		PIST A -1
		Stop
	LetsGetStarted:
		TNT1 A 0 A_TakeInventory("StartWeapon")
		PUN1 A 10 A_WeaponReady
		PUNS ABC 1 A_WeaponReady
		PUNS D 7 A_PlaySound("Knuckled")
		PUNS EFED 3 A_WeaponReady
		PUNS I 7 A_PlaySound("Knuckled")
		PUNS JKJI 3 A_WeaponReady
		PUNS CBA 1 A_WeaponReady
		PUN6 DCBA 1	A_WeaponReady
	WarmUp:
		TNT1 A 0 A_TakeInventory("FirstTimePistol")
		PISI C 1 Offset(55,59)
		PISI C 1 Offset(34,48)
		PISI C 1 Offset(15,37)
		PISI C 1 Offset(6,32)
		PISI DEFG 1 A_WeaponReady
		PISI G 5 A_WeaponReady
		TNT1 A 0 A_PlaySound("prel")
		PISW ABCDEFGGG 1
		PISR QRS 1
		PISI KLMOPQ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISBACK",CHAN_WEAPON)
		PISI Q 1 Offset(-2,35) A_WeaponReady
		PISI Q 1 Offset(-3,37) A_WeaponReady
		PISI R 10 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISFORW",CHAN_WEAPON)
		PISI STU 1 A_WeaponReady
		PISI DCBA 1 A_WeaponReady
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIf(CallACS("WeaponsWarmUpACS")==false,3)
		TNT1 A 0 A_JumpIfInventory("StartWeapon",1,"LetsGetStarted")
		TNT1 A 0 A_JumpIfInventory("FirstTimePistol",1,"WarmUp")
		TNT1 A 0 A_PlaySound("PISSEL",CHAN_WEAPON)
		TNT1 AAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		P1SS A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS A 0
		"####" ABCD 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",1)
		TNT1 A 0 A_Takeinventory("TossMine",1)
		TNT1 A 0 A_Takeinventory("PlaceMine",1)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		P1SS D 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS D 0
		"####" DCBA 1
		TNT1 A 1 A_Lower
		Wait
	NoAmmo:
		PISG A 1
		PISG A 10 A_PlaySound("DRYFIRE",1)
		Goto Ready
	Fidget://R31 Fidget Animation
		P1SI A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI A 0
		"####" ABCDEFG 1 A_WeaponReady
		"####" HIJKLMOPQ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISBACK",CHAN_WEAPON)
		P1SI Q 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI Q 0
		"####" Q 1 Offset(-2,35) A_WeaponReady
		"####" Q 1 Offset(-3,37) A_WeaponReady
		"####" R 10 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISFORW",CHAN_WEAPON)
		P1SI S 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI S 0
		"####" STU 1 A_WeaponReady
		"####" DCBA 1 A_WeaponReady
		Goto Ready
	MineThrowFlash:
		P1SS A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS A 0
		"####" DCBA 1
		TNT1 A 18
		P1SS D 0 A_JumpIfInventory("IsSilencer",1,2)
		PISS D 0
		"####" ABCD 1
		Stop
	KickingFlash:
		P1SI A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI A 0
		"####" ABCDEF 1
		"####" G 4
		"####" FEDCBA 1
		Stop
	AirKickingFlash:
		P1SI A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI A 0
		"####" ABCDEF 1
		"####" G 6
		"####" FEDCBA 1
		Stop
	SlideKickingStart:
		P1SI A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISI A 0
		"####" ABCDEF 1
		"####" G 10
	SlideKickingEnd:
		"####" G 4
		"####" FEDCBA 1
		Stop
	v1Ready:
	Ready:
		P1SG A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISG A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		Goto AmmoCheck
	ResumeFire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff",0)
		TNT1 A 0 A_JumpIfInventory("IsSilencer",1,"SilenceFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("PISTOLF",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.5, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 Bright
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		PISF D 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		PISG A 7 A_WeaponReady(1|WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	SilenceFire:
		TNT1 A 0 A_PlaySound("PISTOLF3",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		P1SF A 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.5, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		P1SF B 2 Bright
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		P1SF CC 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		P1SF D 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		P1SG A 7 A_WeaponReady(1|WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PistolSilencerPickup",1,2)
		TNT1 A 0 A_Print("You need a pistol silencer! This can be found in backpacks")
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("IsSilencer",1,"StopAltFire")
		TNT1 A 0 A_GiveInventory("IsSilencer",1)
		PISA ABCDEFGHIJKLMN 1
		PISA O 3
		TNT1 A 0 A_PlaySound("SILEIN")
		PISA PQR 1
		PISA S 3
		PISA TUVWXYZ 1
		P1SA AB 1
		Goto Ready
	StopAltFire:
		TNT1 A 0 A_TakeInventory("IsSilencer",1)
		P1SA BA 1
		PISA ZYXWVT 1
		PISA S 3
		TNT1 A 0 A_PlaySound("SILEOUT")
		PISA RQP 1
		PISA O 3
		PISA NMLKJIHGFEDCBA 1
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"Fidget")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("reload")
		P1SR A 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR A 0
		"####" ABCDEFGH 1
		TNT1 A 0 A_FireCustomMissile("EmptyPistolClipSpawn",-5,0,-10,-20)
		P1SR H 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR H 0
		"####" I 10
		TNT1 A 0 A_PlaySound("prel")
		P1SR J 0 A_JumpIfInventory("IsSilencer",1,2)
		PISR J 0
		"####" JKLMNOPPP 1
		"####" QRSTUV 1
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_GiveInventory("PistolAmmo",1)
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"Ready")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Reloadloop")
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	}
}