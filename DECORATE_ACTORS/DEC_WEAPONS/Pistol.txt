ACTOR PistolSpawner : CustomInventory Replaces Pistol		//R31 added PISTOL SPAWNER
{
Scale 0.82
Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Pistol",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PistolTactical",1)
		Stop
	}
}

Actor 64Pistol : DoomWeapon		//R31 Replaces Pistol MOVED TO PISTOL SPAWNER
{
Scale 0.82
Weapon.SelectionOrder 1900
Weapon.AmmoUse 1
Weapon.AmmoGive 10		//R31 WAS 20
Weapon.AmmoType "Clip"
Obituary "$OB_MPPISTOL"
+WEAPON.WIMPY_WEAPON
+WEAPON.NOAUTOFIRE
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
VisibleToPlayerClass "64DoomPlayer"												//R11 added
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"	//R11 added //R15 added Tactical64DoomPlayer_P	//R28 added MODERN
+INVENTORY.RESTRICTABSOLUTELY
	states
	{
	Ready:
		PISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 added
		Loop
	Select:
		PISG A 1 A_Raise
		wait
	/*SelectAnimation:
		PISS ABCD 1
		Goto Ready*/
	Deselect:
		PISG A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_PlaySound("PISTOLF",1) 	//R31 was EAGLEFI
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 BRIGHT
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		PIST A -1
		Stop
	}
}

//R15 added TACTICAL PISTOL ADDITIONS BELOW
ACTOR PistolAmmo : Ammo
{
Inventory.icon "PISTA0"
Ammo.BackPackAmount 0
Ammo.BackpackMaxAmount 10
Inventory.Amount 0
Inventory.MaxAmount 10
+IGNORESKILL
}

Actor 64PistolTACTICAL : Brutal64Weapon
{
Weapon.SelectionOrder 1901		//R21 WAS 1900
Weapon.AmmoUse1 1				//R25 WAS 0
Weapon.AmmoGive1 10
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Weapon.AmmoType2 "PistolAmmo"
Obituary "$OB_MPPISTOL"
//+WEAPON.WIMPY_WEAPON		//R25 removed
+WEAPON.NOAUTOFIRE
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
Scale 0.82
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH		//R25 added
	states
	{
	Spawn:
		PIST A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("PISSEL",CHAN_WEAPON)
		PISG A 1 A_Raise
		PISG AAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		PISS ABCD 1
		Goto Ready
	Deselect:
		PISS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	NoAmmo:
		PISG A 1
		PISG A 10 A_PlaySound("DRYFIRE",1)
		Goto Ready
	Fidget://R31 Fidget Animation
		PISI ABCDEFG 1 A_WeaponReady
		PISI HIJKLMOPQ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISBACK",CHAN_WEAPON)
		PISI Q 1 Offset(-2,35) A_WeaponReady
		PISI Q 1 Offset(-3,37) A_WeaponReady		
		PISI R 10 A_WeaponReady
		TNT1 A 0 A_PlaySound("PISFORW",CHAN_WEAPON)
		PISI STU 1  A_WeaponReady
		PISI DCBA 1 A_WeaponReady
		Goto Ready
	KickingFlash:	//r31 new kick animations
		PISI ABCDEF 1
		PISI G 4
		PISI FEDCBA 1
		Stop
	AirKickingFlash:
		PISI ABCDEF 1
		PISI G 6
		PISI FEDCBA 1
		stop
	SlideKickingStart:
		PISI ABCDEF 1
		PISI G 10
	SlideKickingEnd:
		PISI G 4
		PISI FEDCBA 1
		stop
	v1Ready:
	Ready:
		PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
//		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 added
		Loop
	Fire:
		TNT1 A 0 a_JumpifInventory("PistolAmmo",1,1)	//R24.2 WAS JUMP 2
		Goto Reload
		TNT1 A 0 a_JumpifInventory("clip",1,1)			//R25 added
		Goto Fire1										//R25 added
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_GiveInventory("Clip",1)				//R25 added
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("PISTOLF",CHAN_WEAPON)		//R31 was EAGLEFI
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 BRIGHT
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1 | WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	Fire1:		//R25 NEW FIRE1
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("PISTOLF",CHAN_WEAPON)		//R31 was EAGLEFI
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2 BRIGHT
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1 | WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:			//R26 REVISED RELOAD ANIMATION OFFSETS
//		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"Fidget")	//R31 Fidget Animation
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)					//R30.8 was jump 3
		Goto NoAmmo
		tnt1 A 0 A_playsound("reload")		//R31 RELOAD FROM IDDQD1337
		PISR ABCDEFGH 1
		TNT1 A 0 A_FireCustomMissile("EmptyPistolClipSpawn",-5,0,-10,-20)		//R30.10 added
		PISR I 10
		TNT1 A 0 A_PlaySound("prel")
		PISR JKLMNOPPP 1
		PISR QRSTUV 1
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_GiveInventory("PistolAmmo",1)
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"Ready")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Reloadloop")
		Goto Ready
	}
}