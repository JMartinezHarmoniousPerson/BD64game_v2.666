Actor LasergunSpawner : CustomInventory 9351
{
//$category "Game/BD64_Weapons
SpawnID 180
Scale 0.9
Inventory.PickupMessage "You got the Experimental Laser Gun"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		LGUN C 0 NoDelay A_JumpIf(CallACS("D64WeaponsOnlyReplacer")==1,"SpawnPlasmaRifle")	//D64 weapon only
		LGUN C 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LGUN B 0
		"####" "#" 1
		"####" "#" 2
		"####" "#" 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		"####" "#" 2
		"####" "#" 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		"####" "#" 2
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("Lasergun")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("LasergunTactical")
		Stop
	SpawnPlasmaRifle:
		TNT1 A 0 A_SpawnItemEx("PlasmaRifleSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERTID,0,tid)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("Lasergun")
		TNT1 A 0 A_SpawnItemEx("LasergunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor Lasergun : DoomWeapon
{
Scale 0.9
Weapon.SelectionOrder 200
Inventory.PickupMessage "You got the Experimental Laser Gun"
AttackSound "weapons/AlphaLZRGker"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Inventory.Icon LGUNB0
Tag "Laser Gun"
Weapon.SlotNumber 9
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		LGUN B -1
		Stop
	Ready:
		HDLG A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG A 0
		"####" A 1 A_WeaponReady
		Loop
	Select:
		HDLG A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG A 0
		"####" A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		HDLG A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG A 0
		"####" A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_PlaySound("LSSTART",2)
		HDLG B 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG B 0
		"####" BCD 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
	Hold:
		TNT1 A 0 A_PlaySound("LSLoop",CHAN_WEAPON,1,1)
		HDLG E 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG E 0
		"####" E 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0)
		"####" D 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		"####" E 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0)
		"####" A 0 A_Refire
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_PlaySound("LSStop",2)
		"####" A 0 A_ZoomFactor(1.0)
		"####" FGH 1 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		"####" A 2
		Goto Ready
	}
}

Actor LasergunTactical : Brutal64Weapon
{
Scale 0.9
Weapon.SelectionOrder 200
Inventory.PickupMessage "You got the Experimental Laser Gun"
AttackSound "weapons/AlphaLZRGker"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Inventory.Icon LGUNB0
Tag "Laser Gun"
Weapon.SlotNumber 9
+WEAPON.ALT_USES_BOTH
+WEAPON.NOALERT
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	MineThrowFlash:
		HDLR D 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS D 0
		"####" DCBA 1
		TNT1 A 18
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" ABCD 1
		Stop
	KickingFlash:
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" A 0
		"####" DCBA 1
		"####" A 8
		"####" ABCD 1
		Stop
	AirKickingFlash:
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" A 0
		"####" DCBA 1
		"####" A 10
		"####" ABCD 1
		Stop
	SlideKickingStart:
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" A 0
		"####" DCBA 1
		"####" A 18
	SlideKickingEnd:
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" A 0
		"####" A 6
		"####" ABCD 1
		Stop
	Spawn:
		LGUN B -1
		Stop
	v1Ready:
	Ready:
		HDLG A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG A 0
		"####" A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Select:
		TNT1 A 0 A_PlaySound("LSGREADY")
		TNT1 AAAAAAAA 0 A_Raise
	SelectAnimation:
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" ABCD 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",0)
		HDLR A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRS A 0
		"####" DCBA 1
		TNT1 A 0 A_StopSoundEx("WEAPON")
		TNT1 A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 0 A_PlaySound("LSSTART",2)
		HDLG B 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG B 0
		"####" BCD 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
	Hold:
		TNT1 A 0 A_PlaySound("LSLoop",CHAN_WEAPON,1,1)
		TNT1 A 0 A_AlertMonsters
		HDLG E 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG E 0
		"####" E 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0)
		"####" D 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,1)
		"####" E 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0)
		"####" A 0 A_Refire
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_PlaySound("LSStop",2)
		"####" A 0 A_ZoomFactor(1.0)
		"####" FGH 1 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		"####" A 2
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("CELL",15,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,2)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,1)
		Goto Ready
		TNT1 A 0 A_PlaySound("RCHARGE",1,10)
		HDLZ A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LAZF A 0
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,33)
		"####" A 1 Bright Offset(0,32)
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,32)
		"####" A 1 Bright Offset(0,32)
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,32)
	AltHold:
		HDLZ A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LAZF A 0
		"####" A 0 A_JumpIfInventory("CELL",15,2)
		"####" A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		"####" A 1 Bright Offset(0,32)
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,33)
		"####" A 1 Bright Offset(0,32)
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,33)
		"####" A 1 Bright Offset(0,32)
		"####" A 1 Bright Offset(1,32)
		"####" A 1 Bright Offset(2,33)
		"####" A 1 Bright Offset(0,34)
		"####" A 1 Bright Offset(-1,33)
		"####" A 1 Bright Offset(0,33)
		"####" A 0 A_ZoomFactor(0.9)
		"####" A 1 Bright Offset(0,40)
		TNT1 A 0 A_FireCustomMissile("EnergyBlast",0,0,0,-3,0,0)
		TNT1 A 0 A_TakeInventory("CELL",15,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("LaserTrail",0,0,0,0,0,0)
		HDLl A 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LSAL A 0
		"####" ABC 1 Bright A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		"####" A 0 A_PlaySound("RBLAST",1)
		"####" D 1 A_SetPitch(1.0 + pitch)
		TNT1 A 0 A_ZoomFactor(1.0)
		HDLG F 0 A_JumpIf(CallACS("AlternativeLaserSkin")==true,2)
		LZRG F 0
		"####" F 0 A_ReFire
		"####" FGH 1
		"####" AA 2 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		Goto Ready
	}
}

Actor AlphaLZRGkerLaser : FastProjectile 9954
{
Scale 0.6
radius 1
height 1
speed 999
damage 2
Alpha 1
Renderstyle Add
Decal LaserDecal
DamageType Plasma
SeeSound "weapons/AlphaLZRGker"
DeathSound ""
Obituary "%o was fried by %k's laser."
PROJECTILE
+RANDOMIZE
+EXTREMEDEATH
+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 AAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_SpawnItem("RocketFlare")
		TNT1 A 0 A_SpawnItem("DetectFloorLaser")
		TNT1 A 1 A_SpawnItem("DetectCeilLaser")
		Stop
	}
}

Actor AlphaLZRGkerLaserTrail
{
Scale 0.6
Height 4
Radius 2
Speed 0
Damage 0
Alpha 1
RenderStyle Add
PROJECTILE
+RANDOMIZE
	states
	{
	Spawn:
		LAZR ABAB 1 Bright
		Stop
	Death:
		LPUF A 1 Bright
		Stop
	}
}

Actor EnergyBlast : PlasmaBall
{
Scale 0.8
Speed 85
Damage 50
Height 7
Radius 7
Alpha 0.9
RenderStyle Add
Decal MedScorch
DamageType Plasma
+BLOODSPLATTER
+FORCERADIUSDMG
-DONTSPLASH
+NOGRAVITY
+BOUNCEONWALLS
+EXPLODEONWATER
Obituary "%o was barbequed by %k's Lasergun."
SeeSound ""
BounceFactor 0.95
ReactionTime 45
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_CountDown
		TNT1 A 0 A_SpawnItem("RocketFlare")
		TNT1 A 0 A_SpawnItem("LaserTrail")
		LEYS R 1 Bright A_SpawnItem("LaserTrail")
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360))
		TNT1 A 0 A_SpawnItem("Shoque")
		Loop
	Death:
		TNT1 A 0 A_PlaySound("DSFIRXPL",3)
		L2NR AB 3 Bright A_SpawnItem("RocketFlare")
		LENR AB 1 Bright A_SpawnItem("RocketFlare")
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,360))
		TNT1 A 1 A_Explode(100,70)
		TNT1 A 0 A_SpawnItem("Shoque")
		TNT1 A 0 A_SpawnItem("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItem("DetectCeilCraterMed")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		LPUF ABC 9 Bright A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor LaserTrail
{
Scale 2.75
Height 7
Radius 7
Speed 80
Damage 0
Alpha 1
RenderStyle Add
PROJECTILE
+RANDOMIZE
	states
	{
	Spawn:
		LAZR AB 1 Bright A_SpawnItem("RocketFlare")
		Stop
	Death:
		LPUF A 1 Bright
		Stop
	}
}