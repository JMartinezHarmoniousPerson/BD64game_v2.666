Actor 64MineAmmo : Ammo
{
Scale 0.35
Radius 10
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 20
Inventory.Amount 1
Inventory.MaxAmount 10
Inventory.PickupMessage "You got a Multi-Purpose Mine!"
Inventory.Icon PMNED0
Inventory.PickupSound "IPICK"
	States
	{
	Spawn:
		PMNE D -1
		stop
	}
}

ACTOR 64ProximityMine : Brutal64Weapon
{
Scale 0.3
Weapon.AmmoUse 1
Weapon.AmmoGive 1
Weapon.AmmoType "64MineAmmo"
Weapon.SelectionOrder 9999
Obituary "%o was shreded by %k's Mine."
AttackSound "None"
Inventory.PickupSound "IPick"
Inventory.Pickupmessage "You got a Multi-Purpose Mine!"
Tag "Multi-Purpose Mine"
+WEAPON.NOAUTOAIM
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
+WEAPON.ALT_USES_BOTH
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	KickingFlash:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesKickingFlash
		PMNK ABCD 1
		PMNK E 8
		PMNK DCBA 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesAirKickingFlash
		PMNK ABCD 1
		PMNK E 10
		PMNK DCBA 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesSlideKickingStart
		PMNK ABCD 1
		PMNK E 18
	SlideKickingEnd:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesSlideKickingEnd
		PMNK E 6
		PMNK DCBA 1
		Stop
	NoMinesKickingFlash:
		TNT1 A 16
		Stop
	NoMinesAirKickingFlash:
		TNT1 A 18
		Stop
	NoMinesSlideKickingStart:
		TNT1 A 22
	NoMinesSlideKickingEnd:
		TNT1 A 10
		Stop
	Spawn:
		PMNE C -1
		Stop
	Select:
		TNT1 AAAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNS ABCDE 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",1)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto DeselectNoMines
		PMNS EDCBA 1
		TNT1 A 1 A_Lower
		Wait
	DeselectNoMines:
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNE B 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	NoMines:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"SelectAnimation")
		TNT1 A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Goto Ready
	Fire:	//Throw Active Mine, detonate on impact
		PMNF ABCDEFG 1
		TNT1 A 0 A_FireCustomMissile("64ThrownActiveProxMine",0,1,0,0,0,0)
		PMNF HIJKLMNOP 1
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"SelectAnimation")
		Goto NoMines
	AltFire:	//Place Proxy on ground
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNE B 1 Offset(0,33)
		PMNE B 1 Offset(0,34)
		PMNE B 1 Offset(0,35)
		PMNP ABCDE 1
		TNT1 A 0 A_TakeInventory("64MineAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 3
		TNT1 A 0 A_SpawnItemEx("64GroundProxMine",25,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"SelectAnimation")
		Goto NoMines
	}
}

Actor 64ThrownProxMine
{
Scale 0.35
Radius 10
Height 10
Speed 15
Damage(random(2,2))
BounceType "Doom"
BounceFactor 0.3
BounceCount 3
Gravity 0.95
SeeSound "GRNTOSS"		//WAS "MINE001"
DeathSound "MINE002"
PROJECTILE
-NOTELEPORT
-BLOODSPLATTER
-NOGRAVITY
+TELESTOMP
+THRUGHOST
+THRUACTORS
+CANNOTPUSH
+FORCERADIUSDMG
+EXPLODEONWATER
+FIXMAPTHINGPOS
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ThrustThingZ(0,random(12,14),0,1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE C 1
		TNT1 A 0 A_ChangeFlag("THRUACTORS",0)
		Goto Spawn+1
	Death:
	XDeath:
		TNT1 A 0 A_SpawnItemEx("64GroundProxMine",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Stop
	}
}

Actor 64ThrownActiveProxMine
{
Scale 0.35
Radius 10
Height 10
Health 5
Speed 30
Damage(random(2,2))
Gravity 0.7
SeeSound "GRNTOSS"		//WAS "MINE001"
DeathSound "MINE002"
Decal "BigScorch"
Obituary "%o was shreded by %k's Mine."
ReactionTime 3
PROJECTILE
-NOTELEPORT
-BLOODSPLATTER
-NOGRAVITY
+TELESTOMP
+THRUGHOST
+CANNOTPUSH
+FORCERADIUSDMG
+EXPLODEONWATER
+FIXMAPTHINGPOS
+SKYEXPLODE
+BOUNCEONFLOORS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
BounceFactor 0.5
WallBounceFactor 0.25
	States
	{
	Spawn:
		PMNE C 0 NoDelay ThrustThingZ(0,random(12,16),0,1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE CC 2 Light("BlurSphere") A_SpawnItem("RedFlareSpawn",0,6)
	Spawn2:
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE DDDDDDDD 2 Light("BlurSphere") A_SpawnItem("RedFlareSpawn",0,6)
		TNT1 A 0 A_Countdown
		Loop
	Xdeath:
	Death:
		TNT1 A 1 ThrustThingZ(0,10,1,0)
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_AlertMonsters(200)
		TNT1 A 0 A_PlaySound("Explosion",3)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(200,175,XF_NOTMISSILE)
		TNT1 A 0 A_Explode(150,250,XF_NOTMISSILE)
		TNT1 A 0 A_SpawnItem("DetectFloorCrater")
		TNT1 A 0 A_SpawnItem("DetectCeilCrater")
		TNT1 A 0 A_SpawnItem("ExplosionFlareSpawner")
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 A 0 Radius_Quake(2,24,0,15,0)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ShrapnelParticle",0,0,random(0,360),2,random(5,90))
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 AAAAA 1 A_CustomMissile("ExplosionSmoke",1,0,random(0,360),2,random(50,130))
		TNT1 A 1 A_Stop
		Stop
	}
}

Actor 64GroundProxMine : SwitchableDecoration
{
Scale 0.35
Height 6
Radius 10
Health 10
Damage 0
Mass 9999
Activation THINGSPEC_Activate
Tag "Proximity Mine"
PainChance "SuperFists",200
PainChance "Kick",255
PainChance "SuperKick",255
Damagetype Fire
Decal "BigScorch"
Obituary "%o was shreded by %k's Mine."
-SHOOTABLE
-SOLID
-EXPLODEONWATER
+FLOORCLIP
+FRIENDLY
+FIXMAPTHINGPOS
+MOVEWITHSECTOR
+NOBLOOD
+NOICEDEATH
+DONTRIP
+THRUACTORS
+LOOKALLAROUND
+QUICKTORETALIATE
+USESPECIAL
+EXTREMEDEATH
+FORCERADIUSDMG
	States
	{
	Pain:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("64ThrownProxMine",5,0,random(170,190),2,random(20,40))
		Stop
	Active:
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("BlueFlareSpawn",0,6)
		PMNE D 6 Light("MEGASPHERE")
		TNT1 A 0 A_SpawnItem("BlueFlareSpawn",0,6)
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		PMNE D 6 Light("MEGASPHERE")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("BlueFlareSpawn",0,6)
		PMNE D 6 Light("MEGASPHERE")
		PMNE D 4
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_ChangeFLag("SHOOTABLE",0)
		TNT1 A 0 A_SpawnItemEx("64MineAmmo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Stop
	Spawn:
		PMNE D 10
		TNT1 A 0 A_ChangeFlag("SHOOTABLE",1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE DDDD 8 A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItem("YellowFlareSpawn",0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		Goto Spawn+1
	Xdeath:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("RedFlareSpawn",0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_SpawnItem("RedFlareSpawn",0,6)
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("RedFlareSpawn",0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick")
		TNT1 A 0 A_SpawnItem("RedFlareSpawn",0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_ChangeFlag("FLOORCLIP",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Explosion",3)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(200,175,XF_NOTMISSILE)
		TNT1 A 0 A_Explode(150,250,XF_NOTMISSILE)
		TNT1 A 0 A_SpawnItem("DetectFloorCrater")
		TNT1 A 0 A_SpawnItem("DetectCeilCrater")
		TNT1 A 0 A_SpawnItem("ExplosionFlareSpawner")
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 A 0 Radius_Quake(2,24,0,15,0)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ShrapnelParticle",0,0,random(0,360),2,random(5,90))
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 A 0 A_SpawnItem("BarrelExplosionSmokeColumn")
		TNT1 AAAAA 1 A_CustomMissile("ExplosionSmoke",1,0,random(0,360),2,random(50,130))
		TNT1 A 1 A_Stop
		Stop
	}
}