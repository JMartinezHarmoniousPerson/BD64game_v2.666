Actor SuperShotgunSpawner : CustomInventory Replaces SuperShotgun
{
Scale 0.87
Inventory.PickupMessage "$GOTSHOTGUN2"	//R8.3 WAS $GOTSHOTGUN
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SGN2 A -1
		Stop
	Pickup:
		TNT1 A 0 A_Jumpifinventory("IsTacticalClass",1,"Pickup2")	//R3 added
		TNT1 A 0 A_Jumpifinventory("IsModern",1,"Pickup2")			//R27.1 added
		TNT1 A 0 A_GiveInventory("64SuperShotgun")
		Stop
	Pickup2:													//R3 added
		TNT1 A 0 A_GiveInventory("64SuperShotgunTactical",1)	//R3 added
		Stop
	}
}

Actor 64SuperShotgun : DoomWeapon	//R3 added SSG FOR CLASSICAL MODE
{
Scale 0.87
Weapon.SelectionOrder 400
Weapon.AmmoUse 2
Weapon.AmmoGive 8
Weapon.AmmoType "Shell"
+WEAPON.NOAUTOFIRE
Inventory.PickupMessage "$GOTSHOTGUN2"
Obituary "$OB_MPSSHOTGUN"
Tag "$TAG_SUPERSHOTGUN"
VisibleToPlayerClass "64DoomPlayer"												//R11 added
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 added	//R15 added TACTICAL P	//R28 added MODERN
+INVENTORY.RESTRICTABSOLUTELY													//R12.1 added
	States
	{
	Spawn:		//R21 added
		SGN2 A -1
		Stop
	Ready:
		SSGG A 1 A_WeaponReady
		Wait
	Select:
		SSGG A 1 A_Raise
		Wait
	Deselect:
		SSGG A 1 A_Lower
		Wait
	Fire:
		TNT1 AAAA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 A 0 A_JumpIf(CallACS("VanillaWeapons")==1,"VanillaFire")	//r31 added
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,20,6,"shotpuff")		//R24.2 WAS NUMBEULLETS 16	R26 WAS NUMBULLETS 18
		TNT1 A 0 A_PlaySound("SSGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.75)
		SSGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SSGF C 3 A_SetPitch(pitch-4)
		SSGF DE 1 A_SetPitch(pitch+2.6)
		SSGF F 1 A_SetPitch(pitch+1.2)
		SSGG A 1 A_SetPitch(pitch+0.8)
		SSGG A 1 A_SetPitch(pitch+0.4)
		SSGG BCDEFGH 1
		SSGG I 3
		SSGG JKLMNOP 1
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		TNT1 A 1
		SSGG Q 1
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-5,-15)
		SSGG RR 1
		SSGG S 3
		SSGG TUVWX 1
		TNT1 A 0 A_PlaySound("weapons/sshotl",4)
		SSGG YZ 1
		SSGH ABCD 1
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH EFGHIJKLM 1
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFire:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,20,6,"shotpuff")		//R24.2 WAS NUMBEULLETS 16	R26 WAS NUMBULLETS 18
		TNT1 A 0 A_PlaySound("weapons/sshotf",1)
		SSGF AB 1 Bright
		SSGF E 30
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		SSGF E 5
		SSGF F 4
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-5,-15)
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGF F 10
		TNT1 A 0 A_Refire
		Goto Ready
	}
}

Actor SSGAmmo : Ammo		//R2 NEW SSG RELOAD BY XAMP
{
Inventory.Amount 0
Inventory.MaxAmount 2
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 2
Inventory.Icon "sgn2a0"		//R10 WAS "RIFLA0"	//R27 WAS SHELA0
+IGNORESKILL				//R11
}

Actor FirstTimeSSG : Inventory
{
inventory.maxamount 1
}

Actor 64SuperShotgunTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SelectionOrder 401	//R21 WAS 400
Weapon.AmmoType1 "Shell"
Weapon.AmmoGive1 8			//R11 WAS 8
Weapon.AmmoUse1 1			//R25 WAS 0
Weapon.AmmoType2 "SSGAmmo"
Weapon.AmmoGive2 0			//R10.2 WAS 2
Weapon.AmmoUse2 1			//R25 WAS 0
Inventory.PickupMessage "$GOTSHOTGUN2"
Obituary "$OB_MPSSHOTGUN"
Tag "$TAG_SUPERSHOTGUN"
+INVENTORY.RESTRICTABSOLUTELY	//R12.1 added
+WEAPON.AMMO_CHECKBOTH			//R25 added
+WEAPON.NOAUTOFIRE
	States
	{
	KickingFlash:	//r31 new kick animations
		SSGG BCDEFGH 1
		SSGG I 3
		SSGG HGFEDCB 1
		Stop
	AirKickingFlash:
		SSGG BCDEFGH 1
		SSGG I 8
		SSGG HGFEDCB 1
		Stop
	SlideKickingStart:
		SSGG BCDEFGH 1
		SSGG I 9
	SlideKickingEnd:
		SSGG I 4
		SSGG HGFEDCB 1
		Stop
	Spawn:
		SGN2 A -1
		Stop
	WarmUp:
		TNT1 A 0 A_TakeInventory("FirstTimeSSG")
		SSGG E 1 Offset(48,73)
		SSGG E 1 Offset(46,68)
		SSGG E 1 Offset(40,53)
		SSGG E 1 Offset(25,40)
		SSGG E 1 Offset(16,32)
		SSGG EFGH 1
		SSGG I 3
		SSGG JKLMNOP 1
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		TNT1 A 1
		SSGW A 1
		SSGG RR 1
		SSGG S 3
		SSGW BCDEFGH 1
		TNT1 A 0 A_PlaySound("weapons/sshotl")
		SSGW IJKLM 1
		SSGW OPPQRST 1
		TNT1 A 0 A_PlaySound("weapons/sshotl")
		SSGW UUVWXY 1
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH FGHIJKLM 1
		Goto Ready
	v1Ready:
	Ready:
		SSGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Jumpifinventory("Kicking",1,"DoKick")
		Loop
	Select: //R31 Select Animation Improvement
		TNT1 A 0 A_JumpIf(CallACS("WeaponsWarmUpACS")==false,2)
		TNT1 A 0 A_JumpIfInventory("FirstTimeSSG",1,"WarmUp")
		TNT1 AAAAAAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH EF 1
		TNT1 A 0 A_PlaySound("CLIPINSS",CHAN_WEAPON)
		SSGH GHIJKLM 1
		Goto Ready
	Deselect:
		SSGS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fidget: //R31 Fidget Animation
		SSGR ABCDEF 1 A_WeaponReady
		SSGI AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		SSGI B 15 A_WeaponReady
		SSGI A 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH EFGHIJKLM 1
		Goto Ready
	Fire:
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",2,1)
		Goto AltFire2
		TNT1 A 0 A_TakeInventory("SSGAmmo",2)
		TNT1 AAAA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,20,6,"shotpuff",0)	//R24.2 WAS NUMBEULLETS 16	R26 WAS NUMBULLETS 18
		TNT1 A 0 A_TakeInventory("SSGAmmo",2,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("SSGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.75)
		SSGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SSGF C 3 A_SetPitch(pitch-4)
		SSGF DE 1 A_SetPitch(pitch+2.6)
		SSGF F 1 A_SetPitch(pitch+1.2)
		SSGG A 1 A_SetPitch(pitch+0.8)
		SSGG A 1 A_SetPitch(pitch+0.4)
		Goto Reload
	AltFire:
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",2,1)
		Goto Altfire2
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(7.0,5.0,10,6,"ShotgunPuff",0)			//R24.2 WAS NUMBULLETS 8	R26 WAS NUMBULLETS 9
		TNT1 A 0 A_TakeInventory("SSGAmmo",1)
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		SSGU AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SSGF E 3 A_SetPitch(pitch-2)
		TNT1 A 0 Offset(4,32)
		SSGF E 1 A_SetPitch(pitch+1.3)	//r31 was frame E
		TNT1 A 0 Offset(3,32)
		SSGF F 1 A_SetPitch(pitch+0.6)
		TNT1 A 0 Offset(2,32)
		SSGG A 1 A_SetPitch(pitch+0.4)
		TNT1 A 0 Offset(1,32)
		SSGG A 1 A_SetPitch(pitch+0.2)
		TNT1 A 0 Offset(0,32)
		TNT1 A 0 A_Refire
		Goto Ready
	AltFire2:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(7.0,5.0,10,6,"ShotgunPuff",0)			//R24.2 WAS NUMBULLETS 8	R26 WAS NUMBULLETS 9
		TNT1 A 0 A_TakeInventory("SSGAmmo",1)	//R6 was SetInventory //R24.2 added NOTAKEINFINITE	//RE26.1 WAS ALTFIRE2 LINE 2
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		SSGU CD 1 Bright				//R30.9 added I
		TNT1 A 0 A_ZoomFactor(1.0)
		SSGF E 3 A_SetPitch(pitch-2)
		TNT1 A 0 Offset(-4,32)
		SSGF E 1 A_SetPitch(pitch+1.3)	//r31 was frame E
		TNT1 A 0 Offset(-3,32)
		SSGF F 1 A_SetPitch(pitch+0.6)
		TNT1 A 0 Offset(-2,32)
		SSGG A 1 A_SetPitch(pitch+0.4)
		TNT1 A 0 Offset(-1,32)
		SSGG A 1 A_SetPitch(pitch+0.2)
		TNT1 A 0 Offset(0,32)
		TNT1 A 0 A_Refire
		Goto Reload
	Reload:		//r31 new reload sequence
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",2,"Fidget")	//R31 Fidget Animation
		TNT1 A 0 A_Jumpifinventory("SSGAmmo",1,"Reload1")
		Goto Reload2
	Reload2:
		TNT1 A 0 A_Jumpifinventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Jumpifinventory("Shell",2,1)
		Goto Reload1
		SSGG BCDEFGH 1
		SSGG I 3
		SSGG JKLMNOP 1
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		TNT1 A 1
		SSGG Q 1
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-5,-15)
		SSGG RR 1
		SSGG S 3
		SSGG TUVWX 1
		TNT1 A 0 A_PlaySound("weapons/sshotl",4)
		SSGG YZ 1
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SSGAmmo",2)	//R6 was SetInventory
		SSGH ABCD 1
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH EFGHIJKLM 1
		TNT1 A 0 A_Refire
		Goto Ready
	Reload1://R31 Fixed one shell animation, iddqd 1337
		TNT1 A 0 A_Jumpifinventory("Shell",1,1)
		Goto NoAmmo
		SSGR ABCDEF 1
		TNT1 A 2
		SSGZ A 1
		TNT1 A 0 A_PlaySound("weapons/sshoto")
		TNT1 A 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-5,-15)	//30.9 WAS -10 HORZ,R30.9 added VERTICAL Offset
		SSGZ B 3
		SSGZ CDEFGGGHH 1
		TNT1 A 0 A_PlaySound("weapons/sshotl",4)
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SSGAmmo",1)	//R6 was SetInventory	//R19 WAS 2
		SSGZ IJKL 1
		TNT1 A 2
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		SSGH FGHIJKLM 1
		TNT1 A 0 A_Refire
		Goto Ready
	NoAmmo:
		SSGG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE")
		Goto Ready
	}
}