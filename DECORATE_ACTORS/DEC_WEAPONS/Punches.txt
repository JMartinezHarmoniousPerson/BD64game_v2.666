Actor Kicking : Inventory
{
inventory.maxamount 1
}

Actor IsRunning : Inventory
{
inventory.maxamount 1
}

ACTOR Stamina : Ammo
{
Game Doom
SpawnID 11
Inventory.PickupMessage ""
Inventory.Amount 0
Inventory.MaxAmount 100
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 100
Inventory.Icon ""			//R27 WAS PUN1A0	//R28 WAS PLMZA5
+INVBAR						//R27 ADDDED
+IGNORESKILL
}

Actor 64Fist : Fist Replaces Fist	//R8 MERGED FISTS BACK TOGETHER
{
Weapon.SelectionOrder 3700			//R21 ADDED
+WEAPON.NOALERT						//R9.1 WENT MISSING SOMEWHERE IN EDITING NOW RESTORED //R1 ADDED
+INVENTORY.UNDROPPABLE				//R11 ADDED
Tag "Fists"							//R11 ADDED TAG
Inventory.Icon "PUN1A0"				//R27 ADDED
	States
	{
	KickingFlash:	//r31 new kick animations
		PALT ABC 1
		TNT1 A 10
		PALT CBA 1
		stop
	AirKickingFlash:
		PALT ABC 1
		TNT1 A 12
		PALT CBA 1
		stop
	v1Ready:	
	Ready:
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)						//R31 ADDED ALLOWRELOAD
//		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")			//R31 ADDED
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 ADDED
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 ADDED
		Loop
	Tired:
		PUN1 A 1
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		PUN1 A 10
		Goto Ready
	Select: //R31 Select Animation Improvement (Tactical and Modern only)
		TNT1 A 0 A_JumpifInventory("isTacticalClass",1,4)
		TNT1 A 0 A_JumpifInventory("isModern",1,3)
		PUN1 A 1 A_Raise
		WAIT
	SelectAnimation:
		TNT1 A 1 A_Raise
		TNT1 AAAAAAAAAA 0 A_Raise
		PALT ABC 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpifInventory("isTacticalClass",1,4)
		TNT1 A 0 A_JumpifInventory("isModern",1,3)
		PUN1 A 1 A_Lower
		Wait
	DeselectAnimation:
		PALT CBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)	//R8 ADDED	r31 was jump 1
		TNT1 A 0 A_JumpIfInventory("isModern",1,3)			//r31 added
		Goto punch1											//R8 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",10,1)
		Goto Tired
		TNT1 A 0 A_TakeInventory("Stamina",5)
		tnt1 a 0 A_Jump(128,"Punch2")
	Punch1:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch1")
		PUN1 BCD 1
		PUN1 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		goto Ready
	Punch2:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch2")
		PUN2 BCD 1
		PUN2 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		goto Ready
	BerserkPunch1:
		PUN4 BD 1
		TNT1 A 0 A_TakeInventory("Stamina",5)
		PUN1 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		goto Ready
	BerserkPunch2:
		PUN5 BD 1
		TNT1 A 0 A_TakeInventory("Stamina",5)
		PUN2 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		goto Ready
	Reload: //Knuckle animation (Tactical and Modern only)
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Knuckles")
		TNT1 A 0 A_JumpIfInventory("isModern",1,2)
		Goto Ready
	Knuckles:
		PUNS ABC 1
		PUNS D 7 A_PlaySound("Knuckled")
		PUNS EFED 3
		PUNS I 7 A_PlaySound("Knuckled")
		PUNS JKJI 3
//		PUNS CBA 1 A_TakeInventory("Reloading",1)
		Goto Ready
	Altfire:		//r31 added
		TNT1 A 0 A_jumpifinventory("IsTacticalClass",1,2)
		TNT1 A 0 A_jumpifinventory("IsModern",1,4)
		GOTO READY
		TNT1 A 0 A_jumpifinventory("Stamina",10,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",10)
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkAltPunch2")
		PALT ABC 1
		PALT DEFG 1
		PALT H 3 offset(0,32)
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-7)
		PALT H 2 offset(-1,34)
		PALT GFED 1
		PALT CBA 1
		TNT1 A 0 A_ReFire
		GOTO Ready
	BerserkAltPunch2:
		PALT ABC 1
		PBAL ABCDE 1
		PALT H 1 offset(0,32)
		PALT H 1 offset(-1,33)
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-7)
		PALT H 1 offset(1,34)
		PALT H 1 offset(2,32)
		PALT GFED 1
		PALT CBA 1
		TNT1 A 0 A_ReFire
		GOTO SELECTANIMATION
	DoKick:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)						//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheck")		//R28 ADDED
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)			//R7 ADDED
		Goto Ready													//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheck:								//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)	//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeNormalKick:
		TNT1 A 0 A_GunFlash("KickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	AirKick:
		TNT1 A 0 A_TakeInventory("Stamina",5)		//R17 WAS 20
		TNT1 A 0 A_GunFlash("AirKickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready		
	}
}

ACTOR PunchAttack: FastProjectile
{
Damage(random(5,10))
DamageType Melee
Speed 30
Radius 18
Height 6
Alpha 0.6
RenderStyle Add
//HitObituary "$OB_IMPHIT"
SeeSound "none"
DeathSound "none"
Decal "none"
Obituary "$OB_MPFIST"		//r30.10 added
Projectile
+FORCEXYBILLBOARD
+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
		TNT1 A 1 A_PlaySound("VUP")
		Stop
	Death:
		TNT1 A 0 A_PlaySound("player/cyborg/fist",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(2,6,0,5,0)
		TNT1 A 10
		Stop
	}
}

ACTOR SuperPunchAttack: PunchAttack
{
Damage(2*random(60,80))		//R26.1 ADDED 2*
DamageType SuperFists
Obituary "$OB_MPFIST"		//r30.10 added
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
		TNT1 A 1 A_PlaySound("weapons/gswing")
		Stop
	Death:
		TNT1 A 0 A_PlaySound("FOOTWALL",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("FOOTWALL",4)
		TNT1 A 0 A_PlaySound("SUPERFIST",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	}
}

ACTOR KickAttack: FastProjectile
{
Damage(random(15,25))		//R14 WAS 10,15
DamageType Kick
Speed 30
Radius 18
Height 6
ProjectileKickBack 1000		//R17 ADDED
Alpha 0.6
RenderStyle Add
SeeSound "none"
DeathSound "none"
Decal "none"
//HitObituary "$OB_IMPHIT"
Obituary "%o chewed on %k's foot."		//r30.10 added
Projectile
+FORCEXYBILLBOARD
+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
		TNT1 A 1 //A_PlaySound("weapons/gswing")
		Stop
	Death:
	XDeath:
		TNT1 A 0 A_CheckFloor("DeathOnGround")
		TNT1 A 0 A_PlaySound("FOOTWALL",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(2,6,0,5,0)
		TNT1 A 10
		Stop
	DeathOnGround:
		TNT1 A 0 A_PlaySound("FOOTWALL",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(2,6,0,5,0)
		TNT1 A 0 A_Explode(10,40,0)
		TNT1 A 10
		Stop
	}
}

ACTOR SuperKickAttack: KickAttack		//R10.1 NEW SUPERKICK
{
Damage(2*random(70,90))					//R14 WAS 62,62	//R26.1 ADDED 2*
DamageType SuperKick
ProjectileKickBack 550					//R26.1	//R28.2 WAS 500
Obituary "%o chewed on %k's foot."		//r30.10 added
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
		//TNT1 A 1 A_PlaySound("weapons/gswing")
		Stop
	Death:
		TNT1 A 0 A_PlaySound("FOOTWALL",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("FOOTWALL",4)
		TNT1 A 0 A_PlaySound("SUPERFIST",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	}
}

ACTOR AirKickAttack: KickAttack
{
Damage(random(54,62))
Radius 20
Height 32
Obituary "%o chewed on %k's foot."		//r30.10 added
+NOEXTREMEDEATH
}

ACTOR SuperAirKickAttack: KickAttack
{
Damage(2*random(92,110))				//R26.1 ADDED 2*
DamageType SuperKick
Radius 20
Height 32
Obituary "%o chewed on %k's foot."		//r30.10 added
+EXTREMEDEATH
	States
	{
	Death:
		TNT1 A 0 A_PlaySound("FOOTWALL",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,10,0,5,0)
		TNT1 A 10
		Stop
	XDeath:			//R10.1 NEW SUPERAIRKICK XDEATH
		TNT1 A 0 A_PlaySound("FOOTWALL",4)
		TNT1 A 0 A_PlaySound("SUPERFIST",3)
		TNT1 A 0 A_SpawnItemEx("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	}
}

ACTOR KickAttackDetectDowned: KickAttack	//R11 ADDED
{
Damage(random(2,2))
DamageType LowKick
Speed 40
Radius 4
Height 4
-ACTIVATEIMPACT
+NOBLOOD
+BLOODLESSIMPACT
	States
	{
	Death:
		TNT1 A 1
		Stop
	}
}