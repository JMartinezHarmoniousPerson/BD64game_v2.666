Actor RocketLauncherSpawner : CustomInventory Replaces RocketLauncher
{
Scale 0.82
Inventory.PickupMessage "$GOTLAUNCHER"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		LAUN A 1
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")		//r31 was ACS_ExecuteAlways(119)
		LAUN A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")		//r31 removed
		LAUN A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")			//R28 ADDED
		TNT1 A 0 A_GiveInventory("64RocketLauncher",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64RocketLauncherTactical",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_SpawnItem("64RocketLauncherDM")
		TNT1 A 1000
		Loop
	}
}

Actor 64RocketLauncher : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 2500		//R21 WAS 1000
Weapon.AmmoUse 1
Weapon.AmmoGive 2				//R11 WAS 0
Weapon.AmmoType "RocketAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
VisibleToPlayerClass "64DoomPlayer"		//R11 ADDED
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 ADDED	//R15 ADDED TACTICAL P	//R28 ADDED MODERN
+INVENTORY.RESTRICTABSOLUTELY			//R12.1 ADDED
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		Wait
	Deselect:
		MISG A 1 A_Lower
		Wait
	Select:
		MISG A 1 A_Raise
		Wait
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		MISG B 2 Bright Offset(0,36)
		MISG C 2 Bright Offset(0,37) A_GunFlash
		TNT1 A 0 A_FireMissile
		TNT1 A 0 A_Recoil(2)
		MISG D 1 A_SetPitch(pitch-2, SPF_INTERPOLATE)
		MISG E 1 A_SetPitch(pitch+0.8, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.6, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.2, SPF_INTERPOLATE)
		MISG FGHI 2 Bright
		MISG A 4
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor 64Rocket : Rocket Replaces Rocket
{
scale 0.75
Damage 40		//R13 ADDED
Speed 35		//R13 WAS 50
Radius 4
Height 4
Species "Marines"
Decal "BigScorch"		//R28.2	ADDED	//R28.3 WAS SCORCH
-DEHEXPLOSION
-ROCKETTRAIL
+EXTREMEDEATH
+THRUSPECIES
	States
	{
	Spawn:
		MISL A 1 Bright
		TNT1 A 0 A_PlaySound("DSRLAUN")
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0)
		TNT1 A 0 A_SpawnItem("OldschoolRocketSmokeTrail2")
		TNT1 A 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(160,210),2,random(-30,30))
		Loop
	Death:
		TNT1 A 0 A_SpawnItem("ExplosionFlareSpawner")
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItem("DetectFloorCrater")		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("DetectCeilCrater")		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",2)
		TNT1 A 0 A_PlaySound("EXPLOSION",1)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		MISL BCDEF 2 Bright	//R31 Added frames
		stop
	}
}

Actor AltMode : Inventory	//R31 Added
{
Inventory.MaxAmount 1
}

Actor SisterWeaponSelector : PowerWeaponLevel2	//R31 18/08/22 added for classic tactical r_launcher
{
+UNDROPPABLE
+UNTOSSABLE
-COUNTITEM
-INVBAR
+PERSISTENTPOWER
powerup.duration 2147483647
}

Actor RLAmmo : Ammo	//R31 added by IDDQD_1337
{
Inventory.Amount 0
Inventory.MaxAmount 6
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 6
Inventory.Icon "LAUNA0"
+IGNORESKILL
}

Actor 64RocketLauncherTactical : Brutal64Weapon //R31 Rocket Launcher Tactical and Modern Overhaul
{
Scale 0.82
Weapon.SelectionOrder 2501		//R21 was 10
Weapon.AmmoUse1 1
Weapon.AmmoGive1 2
Weapon.AmmoType1 "RocketAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Weapon.AmmoType2 "RLAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.NOALERT
+WEAPON.AMMO_CHECKBOTH
+WEAPON.PRIMARY_USES_BOTH
Weapon.SisterWeapon "64ClassicRocketLauncherTactical"
	States
	{
	MineThrowFlash:
		RLTS DCBA 1
		TNT1 A 18
		RLTS ABCD 1
		Stop
	KickingFlash:	//r31 new kick animations
		TNT1 B 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto KickNoRockets
		RLTG B 0 A_JumpIfInventory("AltMode",1,2)
		RLTL B 0
		"####" BCDE 1
		"####" E 8
		"####" EDCB 1
		Stop
	KickNoRockets:
		RLTN BCDE 1
		RLTN E 8
		RLTN EDCB 1
		Stop
	AirKickingFlash: //18
		TNT1 B 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto AirKickNoRockets
		RLTG B 0 A_JumpIfInventory("AltMode",1,2)
		RLTL B 0
		"####" BCDE 1
		"####" E 10
		"####" EDCB 1
		Stop
	AirKickNoRockets:
		RLTN BCDE 1
		RLTN E 10
		RLTN EDCB 1
		Stop
	SlideKickingStart: //18
		TNT1 B 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto SlideStartNoRockets
		RLTG B 0 A_JumpIfInventory("AltMode",1,2)
		RLTL B 0
		"####" BCDE 1
		"####" E 17
	SlideKickingEnd:
		TNT1 B 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto SlideEndNoRockets
		RLTG E 0 A_JumpIfInventory("AltMode",1,2)
		RLTL E 0
		"####" E 6
		"####" EDCB 1
		Stop
	SlideStartNoRockets:
		RLTN BCDE 1
		RLTN E 17
	SlideEndNoRockets:
		RLTN E 6
		RLTN EDCB 1
		Stop
	Spawn:
		LAUN A -1
		Stop
	ClassicRLSelected:
		TNT1 A 0 A_GiveInventory("SisterWeaponSelector",1)
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,"ClassicRLSelected")
		RLTG A 0 A_JumpIfInventory("AltMode",1,2)
		RLTL A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("RLAmmo",1,"Ready")
		Goto NoRockets
	NoRockets:
		RLTN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	Select: //R31 Select Animation Improvement(Modern and Tactical)
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,"ClassicRLSelected")
		TNT1 A 0 A_PlaySound("ROCKPKUP")
		TNT1 AAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		RLTS ABCD 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",1)
		TNT1 A 0 A_Takeinventory("TossMine",1)
		TNT1 A 0 A_Takeinventory("PlaceMine",1)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_Takeinventory("AltMode",1)
		RLTS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto Reload
		RLTF A 0 A_JumpIfInventory("AltMode",1,2)
		RLLF A 0
		TNT1 A 0 A_AlertMonsters	//R31 ADDED
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"HeavyFire")
		RLLF AB 3 Bright
		TNT1 A 0 A_FireCustomMissile("64rocket",0,0)
		TNT1 A 0 A_TakeInventory("RLAmmo",1,TIF_NOTAKEINFINITE)
		RLLF CD 3 Bright
		RLLF E 1 A_SetPitch(pitch-2, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLLF F 2 A_SetPitch(pitch+0.8, SPF_INTERPOLATE)
		RLLF G 2 A_SetPitch(pitch+0.6, SPF_INTERPOLATE)
		RLTL A 1
		RLTL A 2
		TNT1 A 0 A_ReFire
		Goto Ready
	HeavyFire:
		RLTF AB 3 Bright
		TNT1 A 0 A_FireCustomMissile("TacticalRocket",0,0)
		TNT1 A 0 A_TakeInventory("RLAmmo",1,TIF_NOTAKEINFINITE)
		RLTF CD 3 Bright
		RLTF F 1 A_SetPitch(pitch-2, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLTF E 1 A_SetPitch(pitch+0.8, SPF_INTERPOLATE)
		RLTF E 1 A_SetPitch(pitch+0.6, SPF_INTERPOLATE)
		RLTF E 23
		RLTF FG 1
		RLTG A 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload:
		RLTG A 0 A_JumpIfInventory("AltMode",1,3)
		RLTL A 0 A_JumpIfInventory("RLAmmo",1,2)
		RLTN A 0 A_Jump(256,2)
		"####" A 0 A_JumpIfInventory("RLAmmo",1,2)
		RLTN A 0
		"####" A 1 A_WeaponReady(WRF_NOFIRE)
		RLTG A 0 A_JumpIfInventory("RLAmmo",6,"NoNeedToReload")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		RLTG A 0 A_PlaySound("ROCKREL")
		RL2R A 0 A_JumpIfInventory("RLAmmo",1,2)
		RLTR A 0
		"####" ABCD 1
		TNT1 A 0 A_FireCustomMissile("RocketCaseSpawn",-20,0,10,-20)
		RLTR EFGH 1
		RLTR IJKLMN 1
		TNT1 A 0 A_PlaySound("ROCKLOAD")
		RLTR OPQRST 1
		RLTR UVWXYZ 1
		RL1R ABCDEF 1
		RLTG A 0 A_JumpIfInventory("AltMode",1,"FinishReload") //Skip laser animation
		RLTG A 3
		Goto FinishReload
	FinishReload:
		RLTG A 0 A_JumpIfInventory("RLAmmo",6,"NoNeedToReload")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("RLAmmo",1)
		TNT1 A 0 A_Takeinventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		Goto FinishReload
	NoNeedToReload:
		RLTG A 0 A_JumpIfInventory("AltMode",1,2)
		RLTL A 0
		"####" A 1 A_Refire
		Goto Ready
	Altfire:	//R31 ADDED
		TNT1 A 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"ReturnToClassic")
		RLTM ABCDEE 1
		RLTM F 1 A_PlaySound("BEP",1)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASEROFF",1)
		RLTM FFGHIJ 1
		Goto Ready
	ReturnToClassic:
		RLTM JIHGFF 1
		RLTM E 1 A_PlaySound("BEP",1)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		TNT1 A 0 A_PlaySound("LASERON",1)
		RLTM EEDBCA 1
		Goto Ready
	}
}

Actor 64ClassicRocketLauncherTactical : Brutal64Weapon //R31 Restored
{
Scale 0.82
Weapon.SelectionOrder 2501		//R21 WAS 10
Weapon.AmmoUse1 1
Weapon.AmmoGive1 0
Weapon.AmmoType1 "RocketAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.NOALERT
+POWERED_UP
Weapon.SisterWeapon "64RocketLauncherTactical"
	States
	{
	MineThrowFlash:
		MISS DCBA 1
		TNT1 A 18
		MISS ABCD 1
		Stop
	KickingFlash:	//r31 new kick animations
		MISS DCBA 1
		MISS A 8
		MISS ABCD 1
		Stop
	AirKickingFlash: //18
		MISS DCBA 1
		MISS A 10
		MISS ABCD 1
		Stop
	SlideKickingStart: //18
		MISS DCBA 1
		MISS A 18
	SlideKickingEnd:
		MISS A 6
		MISS ABCD 1
		Stop
	Spawn:
		LAUN A -1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",1)
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Select: //R31 Select Animation Improvement(Modern and Tactical)
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",1)
		TNT1 A 0 A_PlaySound("ROCKPKUP")
		TNT1 AAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		MISS ABCD 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",1)
		TNT1 A 0 A_Takeinventory("TossMine",1)
		TNT1 A 0 A_Takeinventory("PlaceMine",1)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		MISS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		MISG B 2 Bright Offset(0,36)
		MISG C 2 Bright Offset(0,37) A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"HeavyFire")	//added "Altmode" alternates rocket types
		TNT1 A 0 A_FireCustomMissile("64rocket",0,1)
		MISG D 1 Bright A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 Bright A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.2, SPF_INTERPOLATE)
		MISG FGHI 2
		MISG A 4
		TNT1 A 0 A_ReFire
		Goto Ready
	HeavyFire:
		TNT1 A 0 A_FireCustomMissile("TacticalRocket",0,1)
		MISG D 1 Bright A_SetPitch(pitch-2, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 Bright A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.4, SPF_INTERPOLATE)
		MISG F 1 A_SetPitch(pitch+0.2, SPF_INTERPOLATE)
		MISG FGHI 2
		MISG A 4
		MISR ABCCCCC 2
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Altfire:	//R31 ADDED
		MISR ABCCCCC 2
		TNT1 A 0 A_JumpIfInventory("AltMode",1,"ReturnToClassic")
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		Goto Ready
	ReturnToClassic:
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		Goto Ready
	}
}

Actor TacticalRocket : 64Rocket
{
Scale 1.25
Damage(random(305,310))
Speed 40
Gravity 0.2
+MISSILE
-NOGRAVITY
	States
	{
	Death:
		TNT1 A 0 A_SpawnItem("ExplosionFlareSpawner")
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBaseTactical",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItem("DetectFloorCrater")		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("DetectCeilCrater")		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",2)
		TNT1 A 0 A_PlaySound("EXPLOSION",1)
		TNT1 A 0 Radius_Quake(7,13,0,17,0)	//+5ea
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		MISL BCDEF 2 Bright	//R31 Added frames
		stop
	}
}

Actor 64ExplosionFireBaseTactical : 64ExplosionFireBase	//larger explosion for tactical rocket
{
scale 1.35
}

Actor CyberRocket : 64Rocket
{
Damage 40
Speed 30
FastSpeed 35
Species "none"
-THRUSPECIES
}

//DEATHMATCH STUFF
Actor ISDM : Inventory
{
Inventory.MaxAmount 1
}

Actor 64RocketLauncherDM : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 1100
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Weapon.AmmoType "RocketAmmo"
+WEAPON.CHEATNOTWEAPON
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		Wait
	Deselect:
		MISG A 1 A_Lower
		Wait
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG A 1
		TNT1 A 0 A_PlaySound("ROCKE3",1)
		MISG B 1 A_GunFlash
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_FireCustomMissile("DMRocket",0,1)
		MISG B 1 A_SetPitch(pitch-2)
		MISG B 1 A_SetPitch(pitch+0.8)
		MISG B 1 A_SetPitch(pitch+0.6)
		MISG B 1 A_SetPitch(pitch+0.4)
		MISG B 1 A_SetPitch(pitch+0.2)
		MISG B 1
		MISG A 15
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 2 Bright
		MISF B 2 Bright
		MISF CD 2 Bright
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor DMRocket : 64Rocket
{
Speed 30
Species "none"
-THRUSPECIES
}