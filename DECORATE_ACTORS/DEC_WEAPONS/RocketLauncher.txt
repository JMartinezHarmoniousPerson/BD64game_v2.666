ACTOR RocketLauncherSpawner : CustomInventory Replaces RocketLauncher
{
Scale 0.82
//+INVENTORY.ALWAYSPICKUP		//R12.1 REMOVED
Inventory.PickupMessage "$GOTLAUNCHER"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		LAUN A 1
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")		//r31 was ACS_ExecuteAlways(119)
		LAUN A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")		//r31 removed
		LAUN A -1
		Stop
	Pickup:
//		TNT1 A 0 A_GiveInventory("RocketAmmo",2)					//R12.1 REMOVED
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")			//R28 ADDED
		TNT1 A 0 A_GiveInventory("64RocketLauncher",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64RocketLauncherTactical",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItem("64RocketLauncherDM")
		TNT1 A 1000
		Loop
	}
}

Actor 64RocketLauncher : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 2500		//R21 WAS 1000
Weapon.AmmoUse 1
Weapon.AmmoGive 2				//R11 WAS 0
Weapon.AmmoType "RocketAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
VisibleToPlayerClass "64DoomPlayer"		//R11 ADDED
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 ADDED	//R15 ADDED TACTICAL P	//R28 ADDED MODERN
+INVENTORY.RESTRICTABSOLUTELY			//R12.1 ADDED
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")	//R28.2 ADDED
		TNT1 A 0 A_TakeInventory("Stamina",0)					//R28.2 ADDED
		Loop
	/*SelectAnimation:
		MISS ABCD 1
		Goto Ready*/
	Deselect:
		MISG A 1 A_Lower
		Wait
	Select:
		MISG A 1 A_Raise
		WAIT
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		MISG B 2 bright offset(0,36)
		MISG C 2 bright offset(0,37) A_GunFlash
		tnt1 a 0 A_FireMissile
		tnt1 a 0 A_Recoil(2)
		MISG D 1 A_SetPitch(pitch-2)
		MISG E 1 A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6)
		MISG F 1 A_SetPitch(pitch+0.4)
		MISG F 1 A_SetPitch(pitch+0.2)
		MISG FGHI 2 bright
		MISG A 4
		tnt1 a 0 A_ReFire
		Goto Ready
	Flash:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

actor 64Rocket : Rocket Replaces Rocket
{
scale 0.75
Damage 40		//R13 ADDED
Speed 35		//R13 WAS 50
Radius 4
Height 4
Species "Marines"
Decal "BigScorch"		//R28.2	ADDED	//R28.3 WAS SCORCH
-DEHEXPLOSION
-ROCKETTRAIL
+EXTREMEDEATH
+THRUSPECIES
	States
	{
	Spawn:
		MISL A 1 Bright
		TNT1 A 0 A_PlaySound("DSRLAUN")
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0)
		TNT1 A 0 A_SpawnItem("OldschoolRocketSmokeTrail2")
		TNT1 A 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(160,210),2,random(-30,30))
		Loop
	Death:
		//TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 ADDED
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("BigRicoChet")
		TNT1 A 0 A_SPawnItem("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_playSound("EXPLO3",2)
		TNT1 A 0 A_playSound("EXPLOSION",1)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 10
		stop
	}
}

Actor AltMode : Inventory	//R31 Added
{
inventory.maxamount 1
}

Actor SisterWeaponSelector : PowerWeaponLevel2	//R31 18/08/22 added for classic tactical r_launcher
{
+UNDROPPABLE
+UNTOSSABLE
-COUNTITEM
-INVBAR
+PERSISTENTPOWER
powerup.duration 2147483647
}

ACTOR RLAmmo : Ammo	//R31 added by IDDQD_1337
{
Inventory.Amount 0
Inventory.MaxAmount 6
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 6
Inventory.Icon "LAUNA0"
+IGNORESKILL
}

Actor 64RocketLauncherTactical : DoomWeapon //R31 Rocket Launcher Tactical and Modern Overhaul
{
Scale 0.82
Weapon.SelectionOrder 2501		//R21 was 10
Weapon.AmmoUse1 1
Weapon.AmmoGive1 2
Weapon.AmmoType1 "RocketAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Weapon.AmmoType2 "RLAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
//+WEAPON.CHEATNOTWEAPON		//R11 removed
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 added	//R15 //R28 aded modern
Inventory.ForbiddenTo "64DoomPlayer"	//R11 added
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE	//R31 added
+WEAPON.NOALERT		//R31 added
+WEAPON.AMMO_CHECKBOTH		//R31 18/08/22 added
+WEAPON.PRIMARY_USES_BOTH	//R31 18/08/22 added
Weapon.SisterWeapon "64ClassicRocketLauncherTactical"
	States
	{
	KickingFlash:	//r31 new kick animations
		RLTG B 0 A_JumpifInventory("AltMode",1,2)
		RLTL B 0
		"####" BCDEEEEEEEEEEDCB 1
		Stop
	AirKickingFlash: //18
		RLTG B 0 A_JumpifInventory("AltMode",1,2)
		RLTL B 0
		"####" BCDEEEEEEEEEEEEDCB 1
		Stop
	Spawn:
		LAUN A -1
		Stop
	ClassicRLSelected:
		RLTS DCBA 2
		tnt1 A 0 A_GiveInventory("SisterWeaponSelector",1)
	Ready:
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,"ClassicRLSelected")
		RLTG A 0 A_JumpifInventory("AltMode",1,2)
		RLTL A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 adeed
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 added
		Loop
	Select: //R31 Select Animation Improvement(Modern and Tactical)
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,"ClassicRLSelected")
		TNT1 A 0 A_PlaySound("ROCKPKUP")
		TNT1 A 1 A_Raise
		TNT1 AAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		TNT1 A 0 Offset(0,32)
		RLTS ABCD 1
		RLTG A 0 A_JumpifInventory("AltMode",1,"Ready") //Skip laser animation
	LaserActive:
		RLTG A 6
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_PlaySound("LASERON",1)
		RLTG A 1 Offset(0,32)
		RLTL A 1 Offset(0,33)
		RLTL A 1 Offset(0,32)
		Goto Ready
	Deselect:
		RLTG A 0 A_JumpifInventory("AltMode",1,6) //Skip laser animation
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_PlaySound("LASEROFF",1)
		RLTL A 1 Offset(0,32)
		RLTG A 1 Offset(0,33)
		RLTG A 1 Offset(0,32)
		RLTS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 0 A_JumpIfInventory("RLAmmo",1,1)
		Goto Reload
		RLTF A 0 A_JumpifInventory("AltMode",1,2)
		RLLF A 0
		"####" ABCDE 2 bright
		TNT1 A 0 A_AlertMonsters	//R31 ADDED
		tnt1 a 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpifInventory("AltMode",1,"HeavyFire")
		TNT1 A 0 a_JumpifInventory("RocketAmmo",1,1)
		Goto Fire1
		tnt1 a 0 A_FireCustomMissile("64rocket",0,1)
		TNT1 A 0 A_GiveInventory("RocketAmmo",1)
		RLLF G 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLLF E 1 A_SetPitch(pitch+0.8)
		RLLF E 1 A_SetPitch(pitch+0.6)
		RLLF FG 1 bright
		RLTL A 2
		tnt1 a 0 A_ReFire
		Goto Ready
	Fire1:
		tnt1 a 0 A_FireCustomMissile("64rocket",0,1)
		RLLF G 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLLF E 1 A_SetPitch(pitch+0.8)
		RLLF E 1 A_SetPitch(pitch+0.6)
		RLLF FG 1 bright
		RLTL A 2
		tnt1 a 0 A_ReFire
		Goto Ready
	HeavyFire:
		TNT1 A 0 a_JumpifInventory("RocketAmmo",1,1)
		Goto HeavyFire2
		tnt1 a 0 A_FireCustomMissile("TacticalRocket",0,1)
		TNT1 A 0 A_GiveInventory("RocketAmmo",1)
		RLTF G 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLTF E 1 A_SetPitch(pitch+0.8)
		RLTF E 1 A_SetPitch(pitch+0.6)
		RLTF FG 1 bright
		RLTG A 25
		tnt1 a 0 A_ReFire
		Goto Ready
	HeavyFire2:
		tnt1 a 0 A_FireCustomMissile("TacticalRocket",0,1)
		RLTF G 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		RLTF E 1 A_SetPitch(pitch+0.8)
		RLTF E 1 A_SetPitch(pitch+0.6)
		RLTF FG 1 bright
		RLTG A 25
		tnt1 a 0 A_ReFire
		Goto Ready
	Reload:
		RLTG A 0 A_JumpifInventory("AltMode",1,2)
		RLTL A 0
		"####" A 1 A_WeaponReady(WRF_NOFIRE)
		RLTG A 0 A_JumpIfInventory("RLAmmo",6,"NoNeedToReload")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		RLTG A 0 A_JumpifInventory("AltMode",1,6) //Skip laser animation
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_PlaySound("LASEROFF",1)
		RLTL A 1 Offset(0,32)
		RLTG A 1 Offset(0,33)
		RLTG A 1 Offset(0,32)
		RLTG A 0 A_PlaySound("ROCKREL")
		RLTR ABCD 1
		tnt1 A 0 A_FireCustomMissile("RocketCaseSpawn",-20,0,10,-20)
		RLTR EFGGG 1
		RLTR HIJJJJKLM 1
		tnt1 A 0 A_PlaySound("ROCKLOAD")
		RLTR MNOPQRST 1
		RLTR UVWXYZ 1
		RLT1 AB 1
		RLTG A 0 A_JumpifInventory("AltMode",1,"FinishReload") //Skip laser animation
		RLTG A 3
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_PlaySound("LASERON",1)
		RLTG A 1 Offset(0,32)
		RLTL A 1 Offset(0,33)
		RLTL A 1 Offset(0,32)
		Goto FinishReload
	FinishReload:
		RLTG A 0 A_JumpIfInventory("RLAmmo",6,"NoNeedToReload")
		RLTG A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto Ready
		tnt1 A 0 A_Giveinventory("RLAmmo",1)
		tnt1 A 0 A_Takeinventory("RocketAmmo",1)
		Goto FinishReload
	NoNeedToReload:
		RLTG A 0 A_JumpifInventory("AltMode",1,2)
		RLTL A 0
		"####" A 1 A_Refire
		Goto Ready
	Altfire:	//R31 ADDED
		TNT1 A 0 A_JumpifInventory("AltMode",1,"ReturnToClassic")
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_PlaySound("LASEROFF",1)
		RLTL A 1 Offset(0,32)
		RLTG A 1 Offset(0,33)
		RLTG A 1 Offset(0,32)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		GOTO READY
	ReturnToClassic:
		TNT1 A 0 A_PlaySound("BEP",1)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_PlaySound("LASERON",1)
		RLTG A 1 Offset(0,32)
		RLTL A 1 Offset(0,33)
		RLTL A 1 Offset(0,32)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		GOTO READY
	DoKick:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)					//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheck")	//R28 ADDED
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)		//R7 ADDED
		Goto Ready												//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheck:		//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)		//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeNormalKick:
		TNT1 A 0 A_GunFlash("KickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	AirKick:
		TNT1 A 0 A_TakeInventory("Stamina",5)	//R17 WAS 20
		TNT1 A 0 A_GunFlash("AirKickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	}
}

Actor 64ClassicRocketLauncherTactical : DoomWeapon //R31 Restored
{
Scale 0.82
Weapon.SelectionOrder 2501		//R21 WAS 10
Weapon.AmmoUse1 1
Weapon.AmmoGive1 0				//R12.1 ADDED	//r31 removed
Weapon.AmmoType1 "RocketAmmo"
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
//+WEAPON.CHEATNOTWEAPON		//R11 removed
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 ADDED	//R15 //R28 ADDED MODERN
Inventory.ForbiddenTo "64DoomPlayer"	//R11 added
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE	//R31 added
+WEAPON.NOALERT		//R31 added
+POWERED_UP			//R31 added BY IDDQD_1337
Weapon.SisterWeapon "64RocketLauncherTactical"	//R31 added
	States
	{
	KickingFlash:	//r31 new kick animations
		MISS DCBAAAAAAAAAABCD 1
		Stop
	AirKickingFlash: //18
		MISS DCBAAAAAAAAAAAABCD 1
		Stop
	Spawn:
		LAUN A -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIf(CallACS("ClassicRLACS")==true,2)
		TNT1 A 0 A_TakeInventory("SisterWeaponSelector",1)
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 added
		Loop
	Select: //R31 Select Animation Improvement(Modern and Tactical)
		TNT1 A 0 A_PlaySound("ROCKPKUP")
		TNT1 AAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		MISS ABCD 1
		Goto Ready
	Deselect:
		MISS DCBA 1
		tnt1 A 0 A_TakeInventory("SisterWeaponSelector",1)
		TNT1 A 1 A_Lower
		Wait
	Fire:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		MISG B 2 bright offset(0,36)
		MISG C 2 bright offset(0,37) A_GunFlash
		TNT1 A 0 A_AlertMonsters	//R31 ADDED
		tnt1 a 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.97)
		TNT1 A 0 A_JumpifInventory("AltMode",1,"HeavyFire")	//R31 ADDED(ALTMODE ALTERNATES ROCKET TYPE)
		tnt1 a 0 A_FireCustomMissile("64rocket",0,1)
		MISG D 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6)
		MISG F 1 A_SetPitch(pitch+0.4)
		MISG F 1 A_SetPitch(pitch+0.2)
		MISG FGHI 2 bright
		MISG A 4
		tnt1 a 0 A_ReFire
		Goto Ready
	HeavyFire:
		tnt1 a 0 A_FireCustomMissile("TacticalRocket",0,1)
		MISG D 1 A_SetPitch(pitch-2)
		TNT1 A 0 A_ZoomFactor(1.0)
		MISG E 1 A_SetPitch(pitch+0.8)
		MISG F 1 A_SetPitch(pitch+0.6)
		MISG F 1 A_SetPitch(pitch+0.4)
		MISG F 1 A_SetPitch(pitch+0.2)
		MISG FGHI 2 bright
		MISG A 4
		MISR ABCCCCC 2
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		tnt1 a 0 A_ReFire
		Goto Ready
	Flash:	//R31 New smooth animation(Taken from Smooth Doom by gifty)
		TNT1 A 3 Bright A_Light1
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light2
		TNT1 A 6 Bright
		Goto LightDone
	Altfire:	//R31 ADDED
		MISR ABCCCCC 2
		TNT1 A 0 A_JumpifInventory("AltMode",1,"ReturnToClassic")
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		TNT1 A 0 A_Print("Heavy Payload Rockets Enabled",1)
		TNT1 A 0 A_GiveInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		GOTO READY
	ReturnToClassic:
		TNT1 A 0 A_PlaySound("ROCKLOAD",1)
		TNT1 A 0 A_Print("Regular Rockets Enabled",1)
		TNT1 A 0 A_TakeInventory("AltMode",1)
		MISR DEFGFED 1
		MISR CCCCBA 2
		GOTO READY
	DoKick:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)					//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheck")	//R28 ADDED
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)		//R7 ADDED
		Goto Ready												//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheck:		//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)		//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeNormalKick:
		TNT1 A 0 A_GunFlash("KickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	AirKick:
		TNT1 A 0 A_TakeInventory("Stamina",5)	//R17 WAS 20
		TNT1 A 0 A_GunFlash("AirKickingFlash")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	}
}

actor TacticalRocket : 64Rocket
{
Scale 1.25	//R31 16/08/22 added
Damage(random(305,310))
Speed 40
Gravity 0.2
+MISSILE
-NOGRAVITY
	States	//R31 16/08/22 added new state
	{
	Death:
		//TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBaseTactical",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 ADDED
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 ADDED
		TNT1 A 0 A_SpawnItem("BigRicoChet")
		TNT1 A 0 A_SPawnItem("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_playSound("EXPLO3",2)
		TNT1 A 0 A_playSound("EXPLOSION",1)
		TNT1 A 0 Radius_Quake(7,13,0,17,0)	//+5ea
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 10
		stop
	}
}

Actor 64ExplosionFireBaseTactical : 64ExplosionFireBase	//R31 16/08/22 larger explosion for tactical rocket
{
scale 1.35
}

actor CyberRocket : 64Rocket
{
Damage 40
Speed 30
FastSpeed 35
Species "none"
-THRUSPECIES
}

//DEATHMATCH STUFF
Actor ISDM : Inventory	//R31 REMOVED,REPLACE DM SPAWN
{
inventory.maxamount 1
}

Actor 64RocketLauncherDM : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 1100
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Weapon.AmmoType "RocketAmmo"
+WEAPON.CHEATNOTWEAPON
Inventory.PickupMessage "$GOTLAUNCHER"
Tag "$TAG_ROCKETLAUNCHER"
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")		//R28.2 ADDED
		TNT1 A 0 A_TakeInventory("Stamina",0)						//R28.2 ADDED
		Loop
	Deselect:
		MISG A 1 A_Lower
		Wait
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG A 1
		TNT1 A 0 A_PlaySound("ROCKE3",1)
		MISG B 1 A_GunFlash
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_FireCustomMissile("DMRocket",0,1)
		MISG B 1 A_SetPitch(pitch-2)
		MISG B 1 A_SetPitch(pitch+0.8)
		MISG B 1 A_SetPitch(pitch+0.6)
		MISG B 1 A_SetPitch(pitch+0.4)
		MISG B 1 A_SetPitch(pitch+0.2)
		MISG B 1
		MISG A 15
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 2 Bright
		MISF B 2 Bright
		MISF CD 2 bright
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

actor DMRocket : 64Rocket
{
Speed 30
Species "none"
-THRUSPECIES
}