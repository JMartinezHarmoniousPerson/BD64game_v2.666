//R31 NEW BFGF EFGHI SPRITES FROM IDDQD 1337
Actor BFG9000Spawner : CustomInventory Replaces BFG9000
{
Scale 0.9
//+INVENTORY.ALWAYSPICKUP		//R12.1 removed
Inventory.PickupMessage "$GOTBFG9000"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		BFUG A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64BFG9000")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64BFG9000Tactical")
		Stop
	}
}

Actor 64BFG9000 : DoomWeapon
{
Scale 0.9
Height 20
Weapon.SelectionOrder 2800
Weapon.AmmoUse 40
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Inventory.PickupMessage "$GOTBFG9000"
Tag "$TAG_BFG9000"
+WEAPON.NOAUTOFIRE
VisibleToPlayerClass "64DoomPlayer"												//R11 added
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 added	//R15 added TACTICAL P	//R28 added MODERN
+INVENTORY.RESTRICTABSOLUTELY													//R12.1 added
	States
	{
	Ready:
		TNT1 A 0 A_StopSound(5)									//r31 added
		BFGG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")	//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)					//R28.2 added
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(5)	//R31 added
		BFGG A 1 A_Lower
		Wait
	Select: 
		BFGG A 1 A_Raise
		Wait
	Fire:
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_ZoomFActor(0.9)
		TNT1 A 0 A_SetPitch(pitch-4)
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFActor(1.0)
		BFGF CD 1 Bright
		BFGF EEEFFGHI 1 A_SetPitch(pitch+0.5)
		BFGG A 20
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	}
}

Actor 64BFG9000Tactical : Brutal64Weapon
{
Scale 0.9
Height 20
Weapon.SelectionOrder 2801
Weapon.AmmoUse 1		//R31 WAS 40
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Inventory.PickupMessage "$GOTBFG9000"
Tag "$TAG_BFG9000"
+WEAPON.NOAUTOFIRE
+WEAPON.ALT_USES_BOTH	//R31 added
+WEAPON.NOALERT			//R31 added
+INVENTORY.RESTRICTABSOLUTELY		//R12.1 added
	States
	{
	KickingFlash:
		BFGS DCBA 1
		BFGS A 8
		BFGS ABCD 1
		Stop
	AirKickingFlash:
		BFGS DCBA 1
		BFGS A 10
		BFGS ABCD 1
		Stop
	SlideKickingStart:
		BFGS DCBA 1
		BFGS A 18
	SlideKickingEnd:
		BFGS A 6
		BFGS ABCD 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_StopSound(5)									//r31 added
		BFGG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")	//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)					//R28.2 added
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(5)	//R31 added
		BFGS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Select: //R31 Select Animation Improvement (Tactical and Modern only)
		TNT1 A 0 A_PlaySound("BFGREADY")
		TNT1 AAAAAAAA 0 A_Raise
	SelectAnimation:
		BFGS ABCD 1
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("CELL",40,2)	//R31 added
		TNT1 A 0 A_Print("Not Enough Ammo!",1)	//R31 added
		Goto Ready								//R31 added
		TNT1 A 0 A_AlertMonsters
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_TakeInventory("CELL",39)		//R31 added
		TNT1 A 0 A_ZoomFActor(0.9)
		TNT1 A 0 A_SetPitch(pitch-4)
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFActor(1.0)
		BFGF CD 1 Bright
		BFGF EEEFFGHI 1 A_SetPitch(pitch+0.5)
		BFGG A 20
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	AltFire:	//r31 added altfire
		TNT1 A 0 A_jumpifinventory("IsTacticalClass",1,2)
		TNT1 A 0 A_jumpifinventory("IsModern",1,1)
		Goto Ready
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_BFGSound
		BFGA ABABABABAB 1 Bright
		BFGA ABABABABAB 1 Bright
		BFGA ABABABABAB 1 Bright
		BFGA ABABABABAB 1 Bright	//40 TICS TOTAL
		BFGA C 4 Bright
	AltHold:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("BBFGHOLD",5,1,1)
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		BFGA D 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		BFGA C 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		BFGA D 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		BFGA C 1 Bright
		TNT1 A 0 A_ReFire
		BFGA A 2 Bright
		BFGA A 12 Bright A_StopSound(5)
		Goto Ready
	}
}

Actor 64BFGBall : BFGBall Replaces BFGBall
{
Scale 0.75
Radius 8
Height 8
Renderstyle Normal
DamageType "BFG"
Decal "BFGLIGHTNING"	//R28.2 added
+EXTREMEDEATH
	states
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("BFGFlare")			//R30.10 added
		BFS1 AABB 1 Bright A_SpawnItem("BFGFlare")	//R30.10 added
		Loop
	Death:
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavy",0,0,random(0,360),2,random(10,70))						//r31 was 10 spawn height
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))	//r31 was 10 spawn height
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))		//r31 was 10 spawn height
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))					//r31 was 10 spawn height
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("64BFGFireBase",0,0,random(0,360),2,random(0,360))								//r31 was 10 spawn height
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("64BFGFireBaseSmall",0,0,random(0,360),2,random(0,360))							//r31 was 10 spawn height
		TNT1 AAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))								//r31 was 10 spawn height
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 added
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)		//R28.2 added
		TNT1 A 0 A_SpawnItem("BigRicoChet")
		TNT1 A 0 A_SPawnItem("ExplosionAirDisplacementEffect")
		TNT1 A 0 Radius_Quake(4,8,0,12,0)
		TNT1 A 0 A_Explode(200,180,0)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		BFE1 A 6 Bright
		BFE1 B 7 Bright A_FadeOut(0.142857)
		BFE1 C 1 Bright A_FadeOut(0.142857)
		BFE1 C 3 Bright A_BFGSpray
		BFE1 D 1 Bright A_FadeOut(0.142857)
		BFE1 E 1 Bright A_FadeOut(0.142857)
		BFE1 F 1 Bright A_FadeOut(0.142857)
		Stop
	}
}

Actor 64BFGFireBase
{
Scale 1.2
Speed 6
Radius 1
Height 1
Alpha 0.8
RenderStyle Add
BounceFactor 0.5
+DOOMBOUNCE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+MISSILE
+FORCEXYBILLBOARD
+CLIENTSIDEONLY
+NOINTERACTION
+NOGRAVITY
+THRUACTORS
	States
	{
	Spawn:
		BFE1 ABCDEF 4 Bright
		Stop
	}
}

Actor 64BFGFireBaseSmall
{
Scale 0.8
Speed 12
}

Actor 64BFGExtra : BFGExtra Replaces BFGExtra
{
Renderstyle Normal
DamageType "BFG"
+EXTREMEDEATH
	states
	{
	Spawn:
		BFE2 ABCDEF 2 Bright
		Stop
	}
}

Actor BFGBetaBall : PlasmaBall	//R31 added
{
Scale 0.75
Damage 10
DamageType Extreme
Obituary "%o was obliterated by %k's BFG."
SeeSound ""
Decal MedScorch
+BOUNCEONFLOORS
+BOUNCEONCEILINGS
BounceFActor 0.95
ReactionTime 60
	States
	{
	Spawn:
		BFE2 AGH 2 Bright A_CHECKCEILING("Death")
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("DSFIRXPL",3)
		BFE2 BDF 3 Bright
		TNT1 AAAAA 0 A_CustomMissile("explosionParticleGreenHeavySmall",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_CustomMissile("BigRicoChet")
		TNT1 A 1 A_Explode(100,70)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 9 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor BFGBetaBall2 : BFGBetaBall	//R31 added
{
	States
	{
	Spawn:
		REDB ABC 2 Bright
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("DSFIRXPL",3)
		REDB DEF 3 Bright
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_CustomMissile("BigRicoChet")
		TNT1 A 1 A_Explode(100,70)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 9 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}