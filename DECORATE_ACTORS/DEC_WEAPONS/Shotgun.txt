ACTOR ShotgunSpawner : CustomInventory Replaces Shotgun
{
Scale 0.87
//+INVENTORY.ALWAYSPICKUP														//R12.1
Inventory.PickupMessage "$GOTSHOTGUN"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SHOT A -1
		Stop
	Pickup:
//		TNT1 A 0 A_GiveInventory("Shell",4)										//R12.1
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")						//R28 ADDED
		TNT1 A 0 A_GiveInventory("64Shotgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64ShotgunTactical",1)
		Stop
	}
}

Actor 64Shotgun : DoomWeapon
{
Scale 0.87
Weapon.SelectionOrder 1300
Weapon.AmmoUse 1
Weapon.AmmoGive 8																//R11 WAS 0
Weapon.AmmoType "Shell"
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
VisibleToPlayerClass "64DoomPlayer"												//R11
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 ADDED	//R15 ADDED TACTICAL P	//R28 ADDED MODERN
+INVENTORY.RESTRICTABSOLUTELY													//R12.1
	states
	{
	Ready:
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")					//R28.2
		TNT1 A 0 A_TakeInventory("Stamina",0)									//R28.2
		Loop
	Select:
		SHTG A 1 A_Raise
		WAIT
	SelectAnimation:
		SHTS ABCD 1
		Goto Ready
	Deselect:
		SHTS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:
		SHTG A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		tnt1 a 0 A_JumpIf(CallACS("VanillaWeapons")==1,"VanillaFire")	//r31
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
		SHTG BCDEFG 1
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_PlaySound("STGNPUMP",4)
		SHTG HIJKLMMLKJ 1
		SHTG IHGFEDCB 1
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFire:
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("weapons/shotgf",1)
		SHTF AB 3
		SHTF C 15
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-25)
		SHTF D 7
		SHTF E 5
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		SHOT A -1
		STOP
	}
}

ACTOR ShotgunAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 9
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 9
Inventory.Icon "shota0"															//R10 "RIFLA0"	R27 WAS SHELA0
+IGNORESKILL																	//R11 ADDED
}

Actor HasUsedShell : Inventory
{
inventory.maxamount 1
}

Actor 64ShotgunTACTICAL : DoomWeapon
{
Scale 0.87
Weapon.SelectionOrder 1301														//R21 WAS 13
Weapon.AmmoUse1 1																//R25 WAS 0
Weapon.AmmoGive1 8																//R12.1
Weapon.AmmoType1 "Shell"
Weapon.AmmoUse2 1																//R25 WAS 0
Weapon.AmmoGive2 0
Weapon.AmmoType2 "ShotgunAmmo"
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 ADDED	//R15 ADDED TACTICAL PISTOL	//R28 ADDED MODERN
Inventory.ForbiddenTo "64DoomPlayer"											//R11 ADDED
+INVENTORY.RESTRICTABSOLUTELY													//R12.1
+WEAPON.NOAUTOFIRE																//R31
+WEAPON.AMMO_CHECKBOTH															//R25
+NOALERT																		//R31 ADDED FOR ALTFIRE
	states
	{
	
	KickingFlash:
	SHTG BCDEFGGGGGGFEDCB 1
	stop

	ReloadingKick:	
	SHTR H 13
	stop
	
	AirKickingFlash:
		SHTG BCDEFGGGGGGGGFEDCB 1	
	    stop	
	
	Spawn:
		SHOT A -1
		stop
	Ready:									//R11 ADDED(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)						//R28.2		//r31 was ready
		TNT1 A 0 A_TakeInventory("Stamina",0)									//R28.2
	OkToFire:
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	NoAmmo:
		SHTG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE")
		TNT1 A 0 A_TakeInventory("HasUSedShell",1)								//R17 ADDED
		Goto Ready
	Select:
		SHTG A 1 A_Raise
		WAIT
	SelectAnimation:
		SHTS ABCD 1
		Goto Ready
	Deselect:
		SHTS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fidget:
		SHTG BCDEFG 1 A_WeaponReady
		SHTG H 1 A_PlaySound("STGNPUMP",3)
		SHTG IJ 1 A_WeaponReady
		SHTG KLM 1 A_WeaponReady
		SHTG MLKJ 1 A_WeaponReady
		SHTG IHGFEDCB 1 A_WeaponReady
		TNT1 A 0 A_TakeInventory("Reloading",1)
		Goto Ready		
	Fire:
		SHTG A 1
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)							//R25
		Goto Fire1														//R25
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		//TNT1 A 0 A_giveInventory("HasUsedShell",1)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_GiveInventory("SHELL",1)								//R25
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_AlertMonsters												//R31 ADDED ALERT
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
		Goto Pump
	Fire1:																		//R25 NEW FIRE1
		TNT1 A 0 A_ZoomFactor(0.96)
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"PumpNewShell")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		//TNT1 A 0 A_giveInventory("HasUsedShell",1)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1)
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_AlertMonsters												//R31 ADDED ALERT
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
	Pump://R31 Pump System
//		TNT1 A 0 A_GiveInventory("Pumping",1)	//R31 SWC, NOT USED
		SHTG BCDEFG 1
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG H 1 A_PlaySound("STGNPUMP",3)
		SHTG IJ 1 A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,2)
		TNT1 A 0 A_Giveinventory("HasUsedShell",1)
		Goto InsertingShells
		SHTG KLM 1 A_JumpIfInventory("Reloading",1,"InsertingShells")
		SHTG MLKJ 1 A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_Takeinventory("HasUsedShell",1)
		SHTG IHGFEDCB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto ReadyToFireAgain
	ReadyToFireAgain:
		//SHTG A 6 <-- Old frame
		SHTG A 1 Offset(10,36) // <-- New frames
		SHTG A 1 Offset(9,40) //
		SHTG A 1 Offset(4,40) //
		SHTG A 1 Offset(0,32) //
		TNT1 A 0 A_Refire
		Goto Ready
	//R31 reload code reworked
	ReloadWithNoAmmoLeft:
	Reload:
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"Fidget")						//R17 WAS 8 ,64
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,"ReloadNormally")				//R17 WAS 8 ,64
		TNT1 A 0 A_GiveInventory("HasUSedShell",1)
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		SHTG B 1 //A_GiveInventory("Pumping",1)	//R31 SWC, NOT USED
		SHTG CDEFG 1
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
//		TNT1 A 0 A_GiveInventory("Pumping",1)	//R31 SWC, NOT USED
		SHTR H 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",8,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty.
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("Shell",1)
	InsertShellAnimation:
		SHTR HHHABC 1
		SHTR D 1 A_PlaySound("inshell")
		SHTR EFG 1 A_WeaponReady(WRF_NOBOB)
		SHTR H 0 A_JumpIfInventory("Reloading",1,"PumpNewShell")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickReloading")			//r31 delay for autofiring (Unused,because Shotgun reload code has been reworked)
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"FinishInsertingShells")
		Goto InsertingShells
	CheckIfFinishReload:
		SHTR H 1
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_Giveinventory("ShotgunAmmo",1)
		TNT1 A 0 A_Takeinventory("Shell",1)
		Goto InsertShellAnimation
	FinishInsertingShells:
		SHTG G 1 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_jumpIfInventory("HasUsedShell",1,"PumpNewShell")
		SHTG FEDCBA 1 A_WeaponReady
		TNT1 A 0 A_Takeinventory("Reloading",1)
		Goto Ready
	PumpNewShell:
//		TNT1 A 0 A_GiveInventory("Pumping",1)	//R31 SWC, NOT USED
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG G 1
		TNT1 A 0 A_PlaySound("STGNPUMP",4)
		SHTG HIJKLMMLKJ 1
		TNT1 A 0 A_TakeInventory("HasUSedShell",1)
		SHTG IHGFEDCB 1
		Goto ReadyToFireAgain
	DoKick:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)									//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheck")					//R28
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)						//R7 ADDED
		Goto Ready																//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheck:																//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)									//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeNormalKick:	
		TNT1 A 0 A_GunFlash("KickingFlash")		
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto Ready
	DoKickReloading:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)									//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheckReloading")					//R28
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)						//R7 ADDED
		Goto Ready																//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheckReloading:																//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)									//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickReloadingChecker1")
		Goto InitializeReloadingKick
	LowKickReloadingChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeReloadingKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeReloadingKick:	
		TNT1 A 0 A_GunFlash("ReloadingKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto InsertingShells		
		
	AirKick:
		TNT1 A 0 A_TakeInventory("Stamina",5)	//R17 WAS 20
		TNT1 A 0 A_GunFlash("AirKickingFlash")		
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto Ready


	
	Altfire:																	//R31 NEW ALTFIRE
		SGAA ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltFire2")
		SGAA DEFGHI 1
	AltHold:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltHold2")
		TNT1 A 0 A_jumpifinventory("IsModern",1,4)
		TNT1 A 0 A_jumpifinventory("Stamina",10,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",10)
		TNT1 A 0 A_playsound("fstatk")
		SGAA J 1 A_FireCustomMissile("KickAttack",0,0,0,-7)
		SGAA J 4 Offset(-7,57)
		SGAA J 3 Offset(-3,42)
		SGAA J 2 Offset(-1,35)
		SGAA J 1 Offset(0,34)
		SGAA J 2 Offset(-1,35)
		SGAA J 3 Offset(-3,42)
		SGAA J 4 Offset(-7,57)
		TNT1 A 0 A_Refire
		SGAA KLMN 1
		SGAA CBA 1
		Goto Ready
	AltFire2:
		SGAA DEFGHI 1
	AltHold2:
		TNT1 A 0 A_jumpifinventory("IsModern",1,4)
		TNT1 A 0 A_jumpifinventory("Stamina",5,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",5)
		TNT1 A 0 A_playsound("fstatk")
		SGAA J 1 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		SGAA J 3 Offset(-7,57)
		SGAA J 2 Offset(-3,42)
		SGAA J 1 Offset(-1,35)
		SGAA J 1 offset(0,32)
		SGAA J 1 offset(-1,33)
		SGAA J 1 offset(1,34)
		SGAA J 1 offset(2,32)
		SGAA J 1 Offset(-1,35)
		SGAA J 2 Offset(-3,42)
		SGAA J 3 Offset(-7,57)
		TNT1 A 0 A_ReFire
		SGAA KLMN 1
		SGAA CBA 1
		Goto Ready
	}
}