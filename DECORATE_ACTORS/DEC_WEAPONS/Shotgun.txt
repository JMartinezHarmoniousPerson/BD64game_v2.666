Actor ShotgunSpawner : CustomInventory	// Replaces Shotgun		//R31 replaced by new spawner
{
Scale 0.87
//+INVENTORY.ALWAYSPICKUP		//R12.1 removed
Inventory.PickupMessage "$GOTSHOTGUN"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SHOT A -1
		Stop
	Pickup:
//		TNT1 A 0 A_GiveInventory("Shell",4)						//R12.1 removed
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")		//R28 added
		TNT1 A 0 A_GiveInventory("64Shotgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64ShotgunTactical",1)
		Stop
	}
}

Actor 64Shotgun : DoomWeapon
{
Scale 0.87
Weapon.SelectionOrder 1300
Weapon.AmmoUse 1
Weapon.AmmoGive 8	//R11 WAS 0
Weapon.AmmoType "Shell"
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
VisibleToPlayerClass "64DoomPlayer"		//R11 added
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 added	//R15 added TACTICAL P	//R28 added MODERN
+INVENTORY.RESTRICTABSOLUTELY			//R12.1 added
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")	//R28.2 added
		TNT1 A 0 A_TakeInventory("Stamina",0)					//R28.2 added
		Loop
	Select:
		SHTG A 1 A_Raise
		Wait
	Deselect:
		SHTG A 1 A_Lower
		Wait
	Fire:
		SHTG A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_JumpIf(CallACS("VanillaWeapons")==1,"VanillaFire")	//r31 added
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
		SHTG BCDEFG 1
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_PlaySound("STGNPUMP",4)
		SHTG HIJKLMMLKJ 1
		SHTG IHGFEDCB 1
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFire:
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("weapons/shotgf",1)
		SHTF AB 3
		SHTF C 15
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-25)
		SHTF D 7
		SHTF E 5
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor ShotgunAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 9
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 9
Inventory.Icon "shota0"		//R10 "RIFLA0"	R27 WAS SHELA0
+IGNORESKILL				//R11 added
}

Actor HasUsedShell : Inventory
{
inventory.maxamount 1
}

Actor 64ShotgunTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SelectionOrder 1301		//R21 WAS 13
Weapon.AmmoUse1 1				//R25 WAS 0
Weapon.AmmoGive1 8				//R12.1 added
Weapon.AmmoType1 "Shell"
Weapon.AmmoUse2 1				//R25 WAS 0
Weapon.AmmoGive2 0
Weapon.AmmoType2 "ShotgunAmmo"
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
+INVENTORY.RESTRICTABSOLUTELY	//R12.1 added
+WEAPON.NOAUTOFIRE				//R31 added
+WEAPON.AMMO_CHECKBOTH			//R25 added
+NOALERT						//R31 added FOR ALTFIRE
	States
	{
	KickingFlash:	//r31 new kick animations
		SHTG BCDEF 1
		SHTG G 6
		SHTG FEDCB 1
		Stop
	ReloadingKick:
		SHTR H 14
		Stop
	AirKickingFlash:
		SHTG BCDEF 1
		SHTG G 8
		SHTG FEDCB 1
		Stop
	SlideKickingStart:
		SHTG BCDEF 1
		SHTG G 10
	SlideKickingEnd:
		SHTG G 5
		SHTG FEDCB 1
		Stop
	Spawn:
		SHOT A -1
		Stop
	v1Ready:
	Ready:		//R11 added(WRF_ALLOWRELOAD)
//		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)	//R28.2 added	//r31 was ready
		TNT1 A 0 A_TakeInventory("Stamina",0)				//R28.2 added
	OkToFire:
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	NoAmmo:
		SHTG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE")
		TNT1 A 0 A_TakeInventory("HasUSedShell",1)		//R17 added
		Goto Ready
	Select: //R31 Select Animation Improvement
		TNT1 A 0 A_PlaySound("STGNPUMP",3)
		TNT1 AAAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		SHTS ABCD 1
		Goto Ready
	Deselect:
		SHTS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fidget:
		SHTG BCDEFG 1 A_WeaponReady
		SHTG H 1 A_PlaySound("STGNPUMP",3)
		SHTG IJ 1 A_WeaponReady
		SHTG KLM 1 A_WeaponReady
		SHTG MLKJ 1 A_WeaponReady
		SHTG IHGFEDCB 1 A_WeaponReady
		Goto Ready
	Fire:
		SHTG A 1
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)		//R25 added
		Goto Fire1									//R25 added
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		//TNT1 A 0 A_giveInventory("HasUsedShell",1)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_GiveInventory("SHELL",1)		//R25 added
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_AlertMonsters		//R31 added ALERT
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
		Goto Pump
	Fire1:		//R25 NEW FIRE1
		TNT1 A 0 A_ZoomFactor(0.96)
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"PumpNewShell")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		//TNT1 A 0 A_giveInventory("HasUsedShell",1)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1)
		TNT1 A 0 A_PlaySound("SGFIRE",1)
		TNT1 A 0 A_AlertMonsters		//R31 added ALERT
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 3 A_SetPitch(pitch-2)
		SHTF D 1 A_SetPitch(pitch+0.8)
		SHTF E 1 A_SetPitch(pitch+0.6)
		SHTG A 1 A_SetPitch(pitch+0.4)
		SHTG A 1 A_SetPitch(pitch+0.2)
	Pump://R31 Pump System
		SHTG BCDEFG 1
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG H 1 A_PlaySound("STGNPUMP",3)
		SHTG IJ 1 //A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,2)
		TNT1 A 0 A_Giveinventory("HasUsedShell",1)
		Goto InsertingShells
		SHTG KLM 1 //A_JumpIfInventory("Reloading",1,"InsertingShells")
		SHTG MLKJ 1 //A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_Takeinventory("HasUsedShell",1)
		SHTG IHGFEDCB 1 A_JumpIfInventory("Kicking",1,"DoKick")
//		TNT1 A 0 A_JumpIfInventory("Reloading",1,"InsertingShells")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto ReadyToFireAgain
	ReadyToFireAgain:
		//SHTG A 6 <-- Old frame
		SHTG A 1 Offset(10,36)	// <-- New frames
		SHTG A 1 Offset(9,40)	//
		SHTG A 1 Offset(4,40)	//
		SHTG A 1 Offset(0,32)	//
		TNT1 A 0 A_Refire
		Goto Ready
	//R31 reload code reworked
	Reload:
//		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"Fidget")			//R17 WAS 8,64
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,"ReloadNormally")	//R17 WAS 8,64
		TNT1 A 0 A_GiveInventory("HasUSedShell",1)
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		SHTG B 1
		SHTG CDEFG 1
		SHSR ABC 1
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",8,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("Shell",1)
	InsertShellAnimation:
		SHSR C 3
		SHTR ABC 1
		SHTR D 1 A_PlaySound("inshell")
		SHTR EFGHIJ 1 A_WeaponReady(WRF_NOBOB)
//		SHTR H 0 A_JumpIfInventory("Reloading",1,"PumpNewShell")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickReloading")	//r31 delay for autofiring (Unused due to Shotgun reload code rework)
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"FinishInsertingShells")
		Goto InsertingShells
	CheckIfFinishReload:
		SHSR C 1
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_Giveinventory("ShotgunAmmo",1)
		TNT1 A 0 A_Takeinventory("Shell",1)
		Goto InsertShellAnimation
	FinishInsertingShells:
		SHSR CBA 1
		SHTG G 1
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"PumpNewShell")
		SHTG FEDCBA 1 A_WeaponReady
		Goto Ready
	PumpNewShell:
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG G 1
		TNT1 A 0 A_PlaySound("STGNPUMP",4)
		SHTG HIJKLMMLKJ 1
		TNT1 A 0 A_TakeInventory("HasUSedShell",1)
		SHTG IHGFEDCB 1
		Goto ReadyToFireAgain
	DoKickReloading:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)								//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheckReloading")		//R28 added
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)					//R7 added
		Goto Ready															//R7 added
		TNT1 A 0 A_JumpIfInventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheckReloading:						//R28 added
//		TNT1 A 0 A_Takeinventory("ADSmode",1)	//R11 UNUSED
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickReloadingChecker1")
		Goto InitializeReloadingKick
	LowKickReloadingChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeReloadingKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeReloadingKick:
		TNT1 A 0 A_GunFlash("ReloadingKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto InsertingShells
	Altfire:		//R31 NEW ALTFIRE
		SGAA ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltFire2")
		SGAA DEFGHI 1
	AltHold:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltHold2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,4)
		TNT1 A 0 A_JumpIfInventory("Stamina",10,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",10)
		TNT1 A 0 A_playsound("fstatk")
		SGAA J 1 A_FireCustomMissile("KickAttack",0,0,0,-7)
		SGAA J 4 Offset(-7,57)
		SGAA J 3 Offset(-3,42)
		SGAA J 2 Offset(-1,35)
		SGAA J 1 Offset(0,34)
		SGAA J 2 Offset(-1,35)
		SGAA J 3 Offset(-3,42)
		SGAA J 4 Offset(-7,57)
		TNT1 A 0 A_Refire
		SGAA KLMN 1
		SGAA CBA 1
		Goto Ready
	AltFire2:
		SGAA DEFGHI 1
	AltHold2:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,4)
		TNT1 A 0 A_JumpIfInventory("Stamina",5,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",5)
		TNT1 A 0 A_playsound("fstatk")
		SGAA J 1 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		SGAA J 3 Offset(-7,57)
		SGAA J 2 Offset(-3,42)
		SGAA J 1 Offset(-1,35)
		SGAA J 1 offset(0,32)
		SGAA J 1 offset(-1,33)
		SGAA J 1 offset(1,34)
		SGAA J 1 offset(2,32)
		SGAA J 1 Offset(-1,35)
		SGAA J 2 Offset(-3,42)
		SGAA J 3 Offset(-7,57)
		TNT1 A 0 A_ReFire
		SGAA KLMN 1
		SGAA CBA 1
		Goto Ready
	}
}