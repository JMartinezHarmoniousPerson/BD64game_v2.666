Actor PlasmaRifleSpawner : CustomInventory Replaces PlasmaRifle
{
Scale 0.82
Inventory.PickupMessage "$GOTPLASMA"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PLAS A 1
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")	//r31 was ACS_ExecuteAlways(119)
		PLAS A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		PLAS ABC 2
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")			//R28 added
		TNT1 A 0 A_GiveInventory("64PlasmaRifle",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PlasmaRifleTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUActorS",1)
		TNT1 A 0 A_SpawnItem("64PlasmaRifleDM")
		TNT1 A 1000
		Loop
	}
}

Actor HasPLasmaWeapon : Inventory
{
inventory.maxamount 1
}

Actor 64PlasmaRifle : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 100
Weapon.AmmoUse 1
Weapon.AmmoGive 40		//R11 WAS 0
Weapon.AmmoType "Cell"
Inventory.PickupMessage "$GOTPLASMA"
Tag "$TAG_PLASMARIFLE"
VisibleToPlayerClass "64DoomPlayer"												//R11 added
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"	//R11 added	//R15 added TACTICAL P	//R28 added MODERN
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Select:
		PLSG A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_TakeInventory("HasPLasmaWeapon",1)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)	//R31 SOUND FIX FOR GZ4.8
		PLSG A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_PlaySound("weapon/plasmaidle",CHAN_WEAPON,1,1)	//R31 SOUND FIX FOR GZ4.8
		PLSG ABC 2 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("HasPLasmaWeapon",1)					//R31 LINE MOVED FROM SELECTANIMATION
		PLSF A 1 Bright A_FireCustomMissile("64PlasmaBall",0,1,0,-10)	//r31 added 10
		PLSF B 1 Bright
		PLSF CDE 1
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_JumpIf(CallACS("VanillaWeapons")==0,1)	//r31 added
		Goto Ready											//r31 added
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)	//R31 added
		PLSR ABCDEF 1 //R31 added OFFSET (Unused),Reworked animation
		PLSR F 12
		PLSR FEDCBA 1
		Goto Ready
	Spawn:
		PLAS ABC 2
		Loop
	}
}

Actor PlasmaRifleAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 40		//r31 was 30
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 40	//r31 was 30
Inventory.Icon "plasa0"		//R10 WAS "RIFLA0"	R27 WAS CELLA0
+IGNORESKILL				//R11 added
}

Actor 64PlasmaRifleTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.SelectionOrder 101	//R21 WAS 1
Weapon.AmmoUse1 1			//R25 WAS 0
Weapon.AmmoGive1 40			//R12.1 added
Weapon.AmmoType1 "Cell"
Weapon.AmmoUse2 1			//R25 WAS 0
Weapon.AmmoGive2 0
Weapon.AmmoType2 "PlasmaRifleAmmo"
Inventory.PickupMessage "$GOTPLASMA"
Tag "$TAG_PLASMARIFLE"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH		//R25 added
+WEAPON.NOALERT				//R31 added
+WEAPON.NOAUTOFIRE			//R31 added
	States
	{
	KickingFlash:	//r31 new kick animations
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)	//R31 Cooldown if no ammo
		Goto KickingFlashNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 4
		"####" FEDCBA 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)	//R31 Cooldown if no ammo
		Goto AirKickingFlashNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 6
		"####" FEDCBA 1
		Stop
	KickingFlashNoAmmo:
		3LSR ABCDEF 1
		3LSR G 4
		3LSR FEDCBA 1
		Stop
	AirKickingFlashNoAmmo:
		3LSR ABCDEF 1
		3LSR G 6
		3LSR FEDCBA 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)	//R31 Cooldown if no ammo
		Goto SlideKickingStartNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 10
	SlideKickingEnd:
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)	//R31 Cooldown if no ammo
		Goto SlideKickingEndNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		2LSR A 0
		"####" G 4
		"####" FEDCBA 1
		Stop
	SlideKickingStartNoAmmo:
		3LSR ABCDEF 1
		3LSR G 10
	SlideKickingEndNoAmmo:
		3LSR G 4
		3LSR FEDCBA 1
		Stop
	Select: //R31 Select Animation Improvement (Tactical and Modern only)
		TNT1 A 0 A_PlaySound("PLSDRAW")
		TNT1 AAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		PLSG JKLM 1
		Goto Ready
	Deselect:
		PLSG MLKJ 1
		TNT1 A 0 A_TakeInventory("HasPLasmaWeapon",1)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)	//R31 REVISED TO PLAYSOUND,GZ4.8
		TNT1 A 1 A_Lower
		Wait
	Spawn:
		PLAS ABC 2
		Loop
	v1Ready:
	Ready:
		TNT1 A 0 A_PlaySound("weapon/plasmaidle",CHAN_WEAPON,1,1)	//R31 REVISED TO PLAYSOUND	GZ4.8
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)
		Goto NoAmmo
		PLSG A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)			//R31 Pseudo-Fade Effect
		2LSG A 0
		"####" ABC 2 A_WeaponReady(WRF_ALLOWRELOAD)					//R11 added (WRF_ALLOWRELOAD)	//r31 added frame C
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	NoAmmo:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,"Ready")		//R31 added
		TNT1 A 0 A_StopSound(CHAN_WEAPON)							//31 REVISED TO PLAYSOUND,GZ4.8
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		3LSG A 5 A_WeaponReady(WRF_ALLOWRELOAD)					//R11.1 added WRF_ALLOWRELOAD)	//R31 Updated sprite
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("HasPLasmaWeapon",1)			//R31 LINE MOVED FROM SELECTANIMATION
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_FireCustomMissile("64PlasmaBall",0,0,0,-10)	//r31 added 10
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("PlasmaRifleAmmo",1,TIF_NOTAKEINFINITE)
		PLSF AB 1 Bright
		TNT1 A 0 A_SetPitch(pitch-1.6)
		PLSF CDE 1 A_SetPitch(pitch+0.3)						//r31 added frame E
		TNT1 A 0 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)		//R31 added
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)
		TNT1 A 0 A_JumpIfInventory("PlasmaRIfleAmmo",1,1)		//R31 Cooldown if no ammo
		Goto NoAmmoCooldown
		PLSR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)		//R31 Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 12
		"####" FEDCBA 1
		Goto Ready
	NoAmmoCooldown:
		3LSR ABCDEF 1
		3LSR G 12
		3LSR FEDCBA 1
		Goto NoAmmo
	Reload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",40,"Ready")	//R28 WAS JUMP 64	//r31 was 30
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)						//R31 WAS JUMP 3
		Goto NoAmmo
		TNT1 A 0 A_StopSound(CHAN_WEAPON)					//R31 REVISED TO Stopsound,GZ4.8
		11SR A 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		33SR A 0
		"####" AB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("PLREAD1",CHAN_WEAPON)
		11SR C 0 A_JumpIfInventory("PlasmaRIfleAmmo",21,2)	//R31 Pseudo-Fade Effect
		33SR C 0
		"####" CDEFGHIJ 1
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,"Reloadcell2")
		33SR K 1 A_FireCustomMissile("EmptyCell",-5,0,-10,-15)
		33SR K 10 A_PlaySound("PLREL")
		33SR LMNOPQRSTUVWXYZ 1
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",20,"Reloadcell2")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Reloadcell2
		TNT1 A 0 A_GiveInventory("PlasmaRifleAmmo",1)
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
	Reloadcell2:		//r31 added
		TNT1 A 0 A_FireCustomMissile("EmptyCell",-5,0,-10,-15)
		22SR A 15 A_PlaySound("PLREL")
		22SR BCDEFGHIJ 1
		TNT1 A 0 A_PlaySound("PLREAD2")
		22SR KLMNOPQRSTUVX 1
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",40,"Ready")	//R28 WAS JUMP BY 64	//r31 was 30 AMMO
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_GiveInventory("PlasmaRifleAmmo",1)
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
	AltFire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_FireCustomMissile("PlasmaFlame",0,0,0,-12,0,8)
		TNT1 A 0 A_Takeinventory("PlasmaRifleAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("PlasFire")
		PLSF A 1 Bright Offset(0,26) A_SetAngle(random(1,-1) + angle)
		PLSF B 1 Bright Offset(0,32) A_SetAngle(random(1,-1) + angle)
		TNT1 A 0 A_SetPitch(random(1,-1) + pitch)
		TNT1 A 0 A_Refire
		PLSF CDEEE 3 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)
		Goto Ready
	}
}

Actor 64PlasmaBall Replaces PlasmaBall
{
SpawnID 99
Scale 0.5//R31 was 0.01
Radius 4
Height 4
Speed 35
Damage 6
Alpha 0.9
RenderStyle Add
Damagetype "Plasma"
SeeSound "weapons/plasmaf"
DeathSound "weapons/plasmax"
Obituary "$OB_MPPLASMARIFLE"
Decal SmallerScorch			//R28.3 WAS SCORCH
Projectile
+RANDOMIZE
+MISSILE
+THRUSPECIES
	States
	{
	Spawn:
		PLSS A 1 Bright
	Fly:
		PLSS A 1 Bright
		TNT1 A 0 A_SpawnItem("BlueFlareSmallProjectile")
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail1",(3 *momx)/-35.0,-(3 *momy)/-35.0,0+(3 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)	//R31 was 2 zfloat
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail2",(6 *momx)/-35.0,-(6 *momy)/-35.0,0+(6 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail3",(9 *momx)/-35.0,-(9 *momy)/-35.0,0+(9 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail4",(12*momx)/-35.0,-(12*momy)/-35.0,0+(12*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail5",(15*momx)/-35.0,-(15*momy)/-35.0,0+(15*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail6",(18*momx)/-35.0,-(18*momy)/-35.0,0+(18*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail7",(21*momx)/-35.0,-(21*momy)/-35.0,0+(21*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail8",(24*momx)/-35.0,-(24*momy)/-35.0,0+(24*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail9",(27*momx)/-35.0,-(27*momy)/-35.0,0+(27*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail9",(30*momx)/-35.0,-(30*momy)/-35.0,0+(30*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		Loop
	Death:
		TNT1 A 0 A_SpawnItem("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItem("DetectCeilCraterSmall")
		TNT1 A 0 A_CustomMissile("BluePlasmaFire",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 1 A_SpawnItem("LensFlareTorchBlue",0,0)
		PLSE ABCDEF 3 Bright A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))//R31 added line
		TNT1 AAAAA 9 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor 64ArachnotronPlasma: 64PlasmaBall Replaces ArachnotronPlasma
{
Speed 25
Species "Spiders"
}

Actor PlasmaBallTrail1
{
Scale 0.25
Alpha 0.99
Renderstyle Add
+THRUActorS
+CLIENTSIDEONLY
+NOBLOCKMAP
+NOGRAVITY
+MISSILE
	States
	{
	Spawn:
		PLSS B 2 Bright	//R31 was A
		Stop
	}
}

Actor PlasmaBallTrail2: PlasmaBallTrail1
{
Alpha 0.8
	States	//R31 added
	{
	Spawn:
		PLSS A 2 Bright
		Stop
	}
}

Actor PlasmaBallTrail3: PlasmaBallTrail1
{
Alpha 0.7
}

Actor PlasmaBallTrail4: PlasmaBallTrail2
{
Alpha 0.6
}

Actor PlasmaBallTrail5: PlasmaBallTrail2
{
Alpha 0.5
}

Actor PlasmaBallTrail6: PlasmaBallTrail1
{
Alpha 0.4
}

Actor PlasmaBallTrail7: PlasmaBallTrail2
{
Alpha 0.3
}

Actor PlasmaBallTrail8: PlasmaBallTrail1
{
Alpha 0.2
}

Actor PlasmaBallTrail9: PlasmaBallTrail2
{
Alpha 0.1
}

//DEATHMATCH STUFF
Actor 64PlasmaRifleDM : DoomWeapon
{
Scale 0.82
Weapon.SelectionOrder 100
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
+WEAPON.CHEATNOTWEAPON
Inventory.PickupMessage "$GOTPLASMA"
Tag "$TAG_PLASMARIFLE"
	States
	{
	Select:
		TNT1 AAAAA 1 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		PLSG JKLM 1
		TNT1 A 0 A_GiveInventory("HasPLasmaWeapon",1)
		Goto Ready
	Deselect:
		PLSG MLKJ 1
		TNT1 A 0 A_TakeInventory("HasPLasmaWeapon",1)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)	//R31 SOUND FIX FOR GZ4.8
		TNT1 A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_PlaySound("weapon/plasmaidle",CHAN_WEAPON,1,1)	//R31 SOUND FIX FOR GZ4.8
		PLSG ABC 2 A_WeaponReady
		Loop
	Fire:
		PLSF A 1 Bright A_FireCustomMissile("64PlasmaBallDM",0,1)
		PLSF BCDE 1
		TNT1 A 0 A_ReFire
		Goto Ready
	Spawn:
		PLAS A -1
		Stop
	}
}

Actor 64PlasmaBallDM: 64PlasmaBall
{
Species "None"
-THRUSPECIES
}

//Alt Fire Plasma Thrower
Actor FlamethrowerFireParticlesBlue : FlamethrowerFireParticles
{
Translation "0:255=%[0,0,0]:[0,0,1]"
}

Actor ExplosionParticleHeavyBlue : ExplosionParticleHeavy
{
Translation "0:255=%[0,0,0]:[0,0,1]"
}

Actor TinyBurningPieceBlue : TinyBurningPiece
{
Translation "0:255=%[0,0,0]:[0,0,1]"
}

Actor TinyBurningPieceBlue2 : TinyBurningPiece2
{
Translation "0:255=%[0,0,0]:[0,0,1]"
}

Actor PlasmaFlame
{
Scale 1
Damage(random(5,7))
Speed 35
Height 8
Radius 8
Gravity 1.0
RenderStyle Add
Alpha 1
DamageType "Plasma"
Decal "SmallerScorch"
+MISSILE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+FORCEXYBILLBOARD
+NOBLOOD
+NOBLOODDECALS
+BLOODLESSIMPACT
+NODAMAGETHRUST
-NOGRAVITY
-BLOODSPLATTER
+RIPPER
	States
	{
	Spawn:
		PLSE A 1 Bright
		PLSE G 1 Bright
		PLSE A 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE G 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		PLSE A 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE B 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE H 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE B 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE C 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE I 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE C 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE D 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE J 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE D 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE E 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		TNT1 A 0 A_ChangeFlag("DOOMBOUNCE",1)
		TNT1 A 0 A_CustomMissile("ExplosionParticleHeavyBlue",0,0,random(-30,30),2,random(40,160))
		PLSE K 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE E 1 Bright A_SetScale(1.0,2.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE F 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE L 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		Stop
	Death:
		TNT1 A 0 A_CustomMissile("BluePlasmaFire",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItem("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItem("DetectFloorCraterSmallMotherdemon")
		TNT1 A 0 A_SpawnItemEx("TinyBurningPieceBlue2",random(-25,25),random(-15,15))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPieceBlue",random(-15,15),random(-15,15))
		TNT1 AA 0 A_CustomMissile("FlamethrowerFireParticlesBlue",6,0,random(0,360),2,random(10,90))
		Stop
	}
}